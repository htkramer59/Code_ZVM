/*---------------------------------------------------------------+
| Function : Calculate the number of FBA blocks needed           |
|                                                                |
| File     : SIZE2BLK EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called     : SIZE2BLK <size><si-unit>                          |
| As function: out=size2blk(<size><si-unit>)                     |
|                                                                |
| Comments : <size>      the size you need (in bytes)            |
|            <si-unit>   the si unit you want                    |
|                                                                |
|            si-unit:    K = Kilo  1024 to the power of 1        |
|                        M = Mega                       2        |
|                        G = Giga                       3        |
|                        T = Tera                       4        |
|                        P = Peta                       5        |
|                        E = Exa                        6        |
|                        Z = Zetta                      7        |
|                        Y = Yotta                      8        |
|                                                                |
|            When called as function the return code is the      |
|            number of FBA blocks                                |
|                                                                |
| Example  : size2blk 12g                                        |
| Output   :                                                     |
| 12G = 12 GigaBytes = 25165824 FBA blocks = 3145728 4K blocks   |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20181122 17:02:51                                   |
| Changes  : .                                                   |
|            20250630 H.T.Kramer : Small textual error           |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg size .
Parse Source . how myfn myft .
numeric digits 40 /* To display the full number */
blksize=512       /* Size of FBA block in bytes */
unit=1024         /* unit of 1KiloBytes */
If size=''  Then Call Exit 28
If size='?' Then Do
                 'VMFCLEAR'
                 'PIPE (NAME SIZE2BLK.EXEC:46 END ?)',
                      '< 'myfn myft' *',
                      '| tolabel Parse',
                      '| cons'
                  Call Exit 4
                  End
/*---------------------------------------------------------------+
| Split the si_unit and the size                                  |
+---------------------------------------------------------------*/
si_unit=Substr(Reverse(size),1,1)
size=Reverse(Substr(Reverse(size),2))
/*---------------------------------------------------------------+
| Determine the power factor                                     |
+---------------------------------------------------------------*/
Select
  When si_unit='K' Then Do ; power=1 ; si_pref='Kilo' ; End
  When si_unit='M' Then Do ; power=2 ; si_pref='Mega' ; End
  When si_unit='G' Then Do ; power=3 ; si_pref='Giga' ; End
  When si_unit='T' Then Do ; power=4 ; si_pref='Tera' ; End
  When si_unit='P' Then Do ; power=5 ; si_pref='Peta' ; End
  When si_unit='E' Then Do ; power=6 ; si_pref='Exa'  ; End
  When si_unit='Z' Then Do ; power=7 ; si_pref='Zetta'; End
  When si_unit='Y' Then Do ; power=8 ; si_pref='Yotta'; End
  Otherwise Do
            Say 'SI unit 'si_unit' is out of our league'
            Call Exit 99
            End
End
/*---------------------------------------------------------------+
| Actual calculations                                            |
+---------------------------------------------------------------*/
multi=unit**power                               /* Calculate unit to the power of.... */
fbablocks=Format((multi*size)/blksize,,0)       /* Calculate the number of fba blocks */
fourkblocks=Format((multi*size)/(blksize*8),,0) /* Calculate the number of 4k blocks */
/*---------------------------------------------------------------+
| Decide on how to present the outcome...                        |
+---------------------------------------------------------------*/
If how='FUNCTION' Then Call Exit fbablocks
                  Else Do
                       Say size||si_unit' = 'size si_pref'Bytes =' fbablocks' FBA blocks = 'fourkblocks' 4K blocks'
                       Call Exit
                       End
Exit:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Parse Arg exrc .
If exrc='' | exrc='EXRC' Then exrc=0
Exit exrc
