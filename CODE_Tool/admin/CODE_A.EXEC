/*---------------------------------------------------------------+
| Function : Admin part of the code                              |
|                                                                |
| File     : CODE_A   EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : TALK2    EXEC                                       |
|            CODE     NAMES                                      |
|                                                                |
| Called   : CODE_A   .                                          |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20220326 09:17:51                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg details
Parse Source . . myfn myft .
Select
When myft='EXEC'  Then Call Run_Exec
When myft='XEDIT' Then Call Run_Xedit
Otherwise Nop
End
 
Exit:
/*---------------------------------------------------------------+
| General Exit routine                                           |
+---------------------------------------------------------------*/
Parse Arg exrc errmsg
If Datatype(exrc,'NUM') Then Say errmsg exrc
                        Else exrc=0
Exit exrc
 
 
Run_Xedit:
/*---------------------------------------------------------------+
| Run as XEDIT Macro or Profile                                  |
+---------------------------------------------------------------*/
Select
When Wordpos('MENU',details)<>0   Then Call Proc_Admin
When Wordpos('UNLOCK',details)<>0 Then Call Unlock_Code
When Wordpos('SYNC',details)<>0   Then Call Sync_Code
When Wordpos('DEPL',details)<>0   Then Call Deploy_Code
When Wordpos('REMOVE',details)<>0 Then Call Remove_Code
When Wordpos('ADD',details)<>0    Then Call Add_Code
When Wordpos('REPL',details)<>0   Then Call Replace_Code
When Wordpos('SHOW',details)<>0   Then Call Show_Code
Otherwise Call Display
End
Return
 
 
Sync_Code:
/*---------------------------------------------------------------+
| Synchronise the code over all the repositories                 |
+---------------------------------------------------------------*/
Call GetVars '1'
Call Get_Info
errmsg=''
If Word(curline.3,10)<>'' Then errmsg='Can not SYNC code from A-disk or locked code'
If Word(curline.3,9)=''   Then errmsg='Can NOT SYNC older versions of the code'
If errmsg<>'' Then Call Error errmsg
Else Do
  Call Get_DistList
  line1='%W>>>>%WSynching code:%G'Substr(fnft,1,17)'%Wto:%g'distlist
  line2='%W>>>>%WPress ENTER to start SYNC'
  'SET RESERVED 4 NON NOH 'line1
  'SET RESERVED 5 NON NOH 'line2
  'CURSOR SCREEN 4 44'
  Call Read_Update
  If pfk.0<>0 Then Do
            If pfk.1='QQUIT' Then Do
                             Call Error 'SYNC action aborted'
                             'SET RESERVED 4 OFF'
                             'SET RESERVED 5 OFF'
                             ':1'
                             Return
                             End
                             Else Address XEDIT pfk.1
            End
            Else Do
            If res.0=0 Then nodes=Translate(distlist)
                       Else nodes=res.1
            Address CMS 'TALK2 'server' LOCK 'fnft '(' proj
            Address CMS 'TALK2 'server' SYNC 'fnft '(' proj
            Address CMS 'TALK2 'server' UNLOCK 'fnft '(' proj
            Address CMS 'CODEFTP 'fnft proj nodes
            Address CMS 'TALK2 'server' CLEAN 'Word(fnft,1) proj' (OUT'
            End
  'SET RESERVED 4 OFF'
  'SET RESERVED 5 OFF'
  End
':'oldpos
Return
 
 
Deploy_Code:
/*---------------------------------------------------------------+
| Deploy the code to its final destination                       |
+---------------------------------------------------------------*/
Call GetVars '1'
Call Get_Info
errmsg=''
If Word(curline.3,10)<>'' Then errmsg='Can not DEPLoy code from A-disk or locked code'
If Word(curline.3,9)=''   Then errmsg='Can not DEPLoy old versions of the code'
If errmsg<>'' Then Call Error errmsg
     Else Do
     Call Get_DistList
     line1='%W>>>>%WDeploy code:%G'Substr(fnft,1,17)'%Wto:%g'distlist'%W'
     line2='%W>>>>%WPress ENTER to DEPLoy code'
     'SET RESERVED 4 NON NOH 'line1
     'SET RESERVED 5 NON NOH 'line2
     'CURSOR SCREEN 4 42'
     Call Read_Update
     If pfk.0<>0 Then Do
                 If pfk.1='QQUIT' Then Do
                                  Call Error 'DEPLoy action aborted'
                                  'SET RESERVED 4 OFF'
                                  'SET RESERVED 5 OFF'
                                  ':1'
                                  Return
                                  End
                                  Else Address XEDIT pfk.1
                 End
                 Else Do
                 nodes=''
                 Do c=1 To col.0
                    Select
                    When col.c='42' Then nodes=res.c
                    When col.c='68' Then targ_id=res.c
                    When col.c='78' Then targ_cuu=res.c
                    Otherwise Nop
                    End
                 End c
                 If res.0=0 Then nodes=Translate(distlist)
                            Else nodes=res.1
                   Address CMS 'TALK2 'server' DEPL 'fnft proj Userid() node nodes
                   depfile=rc
                   If depfile Then Do
                      Address CMS 'CODEFTP 'Word(fnft,1)' DEPL* 'proj nodes
                      Address CMS 'TALK2 'server' CLEAN 'Word(fnft,1)' DEPL* (OUT'
 
                   End
            End
     'SET RESERVED 5 OFF'
     'SET RESERVED 4 OFF'
     End
':'oldpos
Return
 
 
Replace_Code:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call GetVars '1'
Call Get_Info
If Wordpos('Locked',curline.3)<>0  Then locked=1
                                  Else locked=0
If Wordpos(Userid(),curline.3)<>0 Then owner=1
                                  Else owner=0
rpl=0
unl=0
If locked & owner Then Do
    Address CMS 'SENDFILE 'fnft' A 'server' (NOLOG NOTYPE'
    If rc=0 Then sf=1
            Else sf=0
    If sf Then Do
          Address CMS 'TALK2 'server' REPLACE 'fnft '(' proj
          rpl=rc
          End
    If rpl Then Do
           Address CMS 'TALK2 'server' UNLOCK 'fnft '(' proj
           unl=rc
           End
    If rpl & unl Then Do
           Address CMS 'ERASE 'fnft' A'
           'CLOCATE :80'
           'COVERLAY 'Copies('_',55)
           End
    End
    Else Call Error' Can not obtain 'fnft' status is LOCKED but not by 'Userid()
':'oldpos
Return
 
 
Reload_Code:
/*---------------------------------------------------------------+
| Reload the code on the server                                  |
+---------------------------------------------------------------*/
Address CMS 'SET EMSG OFF'
Address CMS 'TALK2 'server' RELOAD'
If rc=20026 Then rld=1
            Else rld=0
Address CMS 'SET EMSG ON'
If rld Then Say 'Reload succesful'
       Else Say 'Reload failed'
Return
 
 
Unlock_Code:
/*---------------------------------------------------------------+
| Unlock code                                                    |
+---------------------------------------------------------------*/
Call GetVars '1'
Call Get_Info
lockuser=Reverse(Word(Reverse(curline.3),1))
'SET RESERVED 4 NON NOH      %WFile:%G'Substr(fnft,1,17)'%Wlocked by%G'lockuser
'SET RESERVED 5 NON NOH      %WPress ENTER to UNLOCK the code'
Call Read_Update
If entkey Then Do
          Address CMS 'TALK2 'server' UNLOCK 'fnft' ('proj
          unlock=rc
          If unlock Then 'CLOCATE :80#COVERLAY 'Copies('_',60)
          End
':'oldpos
'SET RESERVED 5 OFF'
'SET RESERVED 4 OFF'
Return
 
 
Remove_Code:
/*---------------------------------------------------------------+
| Remove code                                                    |
+---------------------------------------------------------------*/
Call GetVars '1'
Call Get_Info
lockuser=Reverse(Word(Reverse(curline.3),1))
'SET RESERVED 4 NON NOH      %WErase File:%G'Substr(fnft,1,17)
'SET RESERVED 5 NON NOH      %WPress ENTER to REMOVE the code'
Call Read_Update
If entkey Then Do
Address CMS 'TALK2 'server' REMOVE 'fnft' ('proj
'DELETE 1'
End
':'oldpos
'SET RESERVED 5 OFF'
'SET RESERVED 4 OFF'
Return
 
 
Add_Code:
/*---------------------------------------------------------------+
| Add code                                                       |
+---------------------------------------------------------------*/
Call GetVars '1'
Call Get_Info
lockuser=Reverse(Word(Reverse(curline.3),1))
'SET RESERVED 4 NON NOH      %WAdd File:%g'Copies('_',8)'%G%g'Copies('_',8)'%G'
'SET RESERVED 5 NON NOH      %WPress ENTER to ADD the code'
'CURSOR SCREEN 4 17'
Call Read_Update
If entkey Then Do
            fnft=res.1 res.2
            Address CMS 'PIPE (NAME CODE_A.EXEC:258 END ?)',
                'cms LISTFILE 'fnft' A (NOH ISODATE',        /* Ask this         */
                '| specs w1.2 1 w4;* 20 ~   1 File added~ nw',   /* Add text         */
                '| p: pick 1.3 \== ~DMS~',                   /* Ignore these     */
                '| var newfile',                             /* Hand 2 rexx      */
                '? p:',                                      /* Conn 2 pick      */
                '| count lines',                             /* Count them       */
                '| var error'                                /* Hand 2 rexx      */
            If \error Then Do
                           lock=0;sf=0;add=0;unl=0
                           Address CMS 'TALK2 'server' LOCK 'fnft '(' proj
                           lock=rc
                           If lock Then Do
                               Address CMS 'SENDFILE 'fnft' A 'server' (NOLOG NOTYPE'
                               If rc=0 Then sf=1
                                       Else sf=0
                           If sf Then Do
                               Address CMS 'TALK2 'server' ADD 'fnft '(' proj
                               add=rc
                               End
                           If lock Then Do
                              Address CMS 'TALK2 'server' UNLOCK 'fnft '(' proj
                              unl=rc
                              End
                           If add & unl Then Do
                              Address CMS 'ERASE 'fnft' A'
                              'INPUT 'newfile Subword(TimeStamp(),1,2)
                              ':1'
                              'SORT * a 1 8'
                              End
                           End
 
End
':'oldpos
'SET RESERVED 5 OFF'
'SET RESERVED 4 OFF'
Return
 
 
Show_Code:
/*---------------------------------------------------------------+
| Show the selected code                                         |
+---------------------------------------------------------------*/
Call GetVars '1'
Call Get_Info
Address CMS 'VMLINK 'lang' (ONLY(CODE) NOTYPE .fm'
If rc=0 Then Do
Parse Pull . . wfm .
Queue 'EMSG       N.B. ReadOnly!!'
Address CMS 'XEDIT 'fnft wfm
Address CMS 'VMLINK 'lang' <DET (ONLY(CODE) NOTYPE'
End
Else Call Error 'VMLINK failed with rc: 'rc
':'oldpos
Return
 
 
Get_Info:
/*---------------------------------------------------------------+
| Get info                                                       |
+---------------------------------------------------------------*/
'EXTRACT /CURSOR/LINE'
oldpos=line.1
':'cursor.3
'EXTRACT /CURLINE'
fnft=Subword(curline.3,1,2)
fm=Word(curline.3,10)
Return
 
 
Set_CtlChars:
/*---------------------------------------------------------------+
| Set the XEDIT control characters                               |
+---------------------------------------------------------------*/
ctlchars='% ESCAPE;',
         'Y PROTECT   YELLOW HIGH;y NOPROTECT YELLOW HIGH;',
         'W PROTECT   WHITE  HIGH;w NOPROTECT WHITE  HIGH;',
         'B PROTECT   BLUE   HIGH;b NOPROTECT BLUE   HIGH;',
         'G PROTECT   GREEN  HIGH;g NOPROTECT GREEN  HIGH;',
         'R PROTECT   RED    HIGH;r NOPROTECT RED    HIGH;',
         '$ OFF'
Return
 
 
Display:
/*---------------------------------------------------------------+
| Start of the display here                                      |
+---------------------------------------------------------------*/
Call Set_CtlChars
hdr='%WFileName FileType  Recfm  LRecl    Records     Blocks Date       Time     Gens  Status'
pfkeys='Help Code;',
       'Macro Code_A Remove;',
       'QQUIT;',
       'Macro Code_A Unlock;',
       'Macro Code_A Sync;',
       'Macro Code_A Depl;',
       'Backward;',
       'Forward;',
       'Macro Code_A Add;',
       'Macro Code_A Repl;',
       'Macro Code_A Show;',
       'QQUIT'
pfline='1=Help;2=Remove;3=Quit;4=Unlock;5=Sync;6=Deploy;7=Back;8=Forw;9=Add;10=Replace;11=Show;12=Quit'
Call GetVars '1'
Address CMS 'PIPE (NAME CODE_A.EXEC:362 END ?)',
   'var ctlchars',                               /* Take this        */
   '| split at ;',                               /* Split            */
   '| specs ~SET CTLCHAR~ 1 1;* nw',             /* Compose cmds     */
   '| fi: fanin',                                /* Take in others   */
   '| subcom xedit',                             /* Hand 2 xedit     */
   '?',                                          /* 2nd pipe         */
   'var pfkeys',                                 /* Take this var    */
   '| dup 1',                                    /* Duplicate        */
   '| split at ;',                               /* Split            */
   '| specs ~SET PF~ 1 recno n 1;* nw',          /* Add recno        */
   '| fi:',                                      /* Conn 2 fanin     */
   '?',                                          /* 3rd pipe         */
   'var pfline',                                 /* Take this var    */
   '| split at ;',                               /* Split here       */
   '| specs ~%W~ 1 1;* n',                       /* Add ctlchar      */
   '| change ~=~=%G~',                           /* Add another      */
   '| join * x40',                               /* Make 1 record    */
   '| var pfline'                                /* Hand 2 rexx      */
 
'CMDLINE TOP'
'CURLINE ON 3'
'SCALE ON 4'
'SET RESERVED  3 NON NOH      'hdr
'SET RESERVED -1 NON NOH      'pfline
':1'
Return
 
 
Run_Exec:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Say 'Starting ADMIN code'
Call GetVars '1'
Parse Source . . myfn myft .
'PIPE (NAME CODE_A.EXEC:398 END ?)',
   'cms EXECMAP 'myfn' XEDIT',                   /* Ask CMS          */
   '| nfind DMS',                                /* Ignore this      */
   '| drop first 1',                             /* Remove header    */
   '| count lines',                              /* Count them       */
   '| var exldd'                                 /* Store as boolean */
If \exldd Then Address CMS 'EXECLOAD 'myfn myft' * = XEDIT'
proj=lang
Select
When code=''      & proj=''         Then Call Xedit_Names
When code='ADMIN' & proj=''         Then Call Admin_Menu
When code='ADMIN' & proj='DISTLIST' Then Call Xedit_Names 'LOCATE /:DISTLIST.'
When code='ADMIN' & proj='USERS'    Then Call Xedit_Names 'LOCATE /:USERS.'
When code='ADMIN' & proj='SERVER'   Then Call Xedit_Names 'LOCATE /:SERVER.'
When code='ADMIN' & proj='ENVIRON'  Then Call Xedit_Names 'LOCATE /:ENVIRONMENTS.'
When code='ADMIN' & proj='CLIENT'   Then Call Xedit_Names 'LOCATE /:CLIENT..'
When code='ADMIN' & proj='DEPLOY'   Then Address CMS 'XEDIT CODE DEPLOYMT *'
When code='ADMIN' & proj='RELOAD'   Then Call Reload_Code
When code='PROJECT'  Then Do
               If proj='' Then Call Xedit_Names 'LOCATE /:PROJECTS.'
                          Else Call Show_Project proj
               End
Otherwise Nop
End
If \exldd Then Address CMS 'EXECDROP 'myfn 'XEDIT'
Return
 
 
Proc_Admin:
/*---------------------------------------------------------------+
| Process the items entered thru the admin menu                  |
+---------------------------------------------------------------*/
'EXTRACT /CURSOR/RESERVED *'
action=cursor.1
Select
When action='5' Then Address CMS 'CODE_ADMIN DISTLIST'
When action='6' Then Address CMS 'CODE_ADMIN USERS'
When action='7' Then Address CMS 'CODE_ADMIN SERVER'
When action='8' Then Address CMS 'CODE_ADMIN ENVIRON'
When action='9' Then Address CMS 'CODE_ADMIN CLIENT'
When action='11' Then Address CMS 'XEDIT CODE DEPLOYMT *'
When action='13' Then Address CMS 'CODE_ADMIN RELOAD'
When action='15' Then Do
               Say 'Enter project name:'
               Parse Upper Pull project .
               If project<>'' Then Address CMS 'CODE_ADMIN PROJECT 'project
                              Else Call Error 'No project entered'
               End
Otherwise Nop
End
Return
 
 
Admin_Menu:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call Set_CtlChars
lines='%W Code ADMIN menu;',
'%y;',
'%y;',
'%B  CODE NAMES file:;',
'%y %YDISTLIST %Ggo to Distribution list;',
'%y %YUSERS    %Ggo to Users       section;',
'%y %YSERVER   %Ggo to Server      section;',
'%y %YENVIRON  %Ggo to Environment section;',
'%y %YCLIENT   %Ggo to Client      section;',
'%y;',
'%y %YDEPLOY   %Ggo to CODE DEPLOYMT file;',
'%y;',
'%y %YRELOAD   %GRELOAD code and control files on the server;',
'%y;',
'%y %YProject  %Ggo to Project;',
'%y;',
'%y;'
'PIPE (NAME CODE_A.EXEC:473 END ?)',
   'var lines',
   '| split at ;',
   '| specs recno 1.2 1;* nw',
   '| specs ~SET RESERVED~ 1 w1.1 nw ~NON NOH~ nw w2;* nw',
   '| stack'
Queue 'PREFIX  OFF'
Queue 'CMDLINE OFF'
Queue 'TOFEOF  OFF'
Queue 'CURSOR SCREEN 5 2'
Queue 'SET ENTER ONLY MACRO CODE_A MENU ......'
Address CMS 'XEDIT 'myfn' OUTPUT A (PROFILE 'myfn
Return
 
 
Xedit_Names:
/*---------------------------------------------------------------+
| Xedit the NAMES file and issue a locate command                |
+---------------------------------------------------------------*/
Parse Arg locatecmd
Queue 'CASE M I'
If locatecmd<>'' Then Queue locatecmd
Address CMS 'XEDIT CODE NAMES *'
Return
 
 
GetVars:
/*---------------------------------------------------------------+
| Get the variables from a caller                                |
+---------------------------------------------------------------*/
Parse Arg lvl .
If lvl='' Then lvl=1
Address CMS 'PIPE (NAME CODE_A.EXEC:505 END ?)',
   'rexxvars 'lvl' toload',                      /* Take from caller */
   '| varload'                                   /* Load them        */
Return
 
TimeStamp:
Return Date(S) Time() myfn
 
 
Show_Project:
/*---------------------------------------------------------------+
| Access the target                                              |
+---------------------------------------------------------------*/
Address CMS 'VMLINK 'lang' (ONLY(CODE) NOTYPE .fm'
If rc=0 Then Parse Pull . . wfm .
        Else Call Exit rc' Problem accessing repository'
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
outfile=proj' ADMIN A'
local.0=0
'PIPE (NAME CODE_A.EXEC:526 END ?)',
    'cms LISTFILE * * 'wfm' (NOH ISO',           /* Take listing     */
    '| pick substr 1.1 of w2.1 \== ~-~',         /* Select these     */
    '| pick w1.1 \== ~UPDATE~',                  /* .                */
    '| specs w1.1 1.8',                          /* .                */
            'w2.1 nw.8',                         /* .                */
            'w4;* nw',                           /* .                */
    '| f: fanout',                               /* .                */
    '| append disk code status 'wfm,             /* .                */
    '| pad 17',                                  /* .                */
    '| pick 1.1 \== ~*~',                        /* .                */
    '| fi: faninany',                            /* .                */
    '| sort w1.2',                               /* .                */
    '| join keylen 17',                          /* .                */
    '| pad 140',                                 /* .                */
    '| > 'outfile,                               /* .                */
    '? f:',                                      /* .                */
    '| specs w1.1 1.8 w2.1 nw.8',                /* .                */
    '| sort count w1.1',                         /* .                */
    '| specs 11;* 1 1.10 strip nw.5 right',      /* .                */
    '| fi:'                                      /* .                */
/*---------------------------------------------------------------+
| Release the target                                             |
+---------------------------------------------------------------*/
Address CMS 'VMLINK 'lang' <DET (ONLY(CODE) NOTYPE'
/*---------------------------------------------------------------+
| Start XEDIT session                                            |
+---------------------------------------------------------------*/
Address CMS 'XEDIT 'outfile' (PROFILE 'myfn
Address CMS 'ERASE 'outfile
/*---------------------------------------------------------------+
| Clean up                                                       |
+---------------------------------------------------------------*/
Return
 
 
Read_Update:
/*---------------------------------------------------------------+
| Read updates from the screen...                                |
+---------------------------------------------------------------*/
pfk.0=0
res.0=0
'READ ALL TAG'
Address CMS 'PIPE (NAME CODE_A.EXEC:569 END ?)',
    'stack',                                     /* Take from stack  */
    '| p: pick w1.1 == ~PFK~',                   /* Select these     */
    '| specs w3;* 1',                            /* Keep this        */
    '| stem pfk.',                               /* Hand 2 rexx      */
    '? p:',                                      /* Conn 2 pick      */
    '| p2: pick w1.1 == ~RES~',                  /* Select these     */
    '| f: fanout',                               /* Duplicate recs   */
    '| specs w4;* 1',                            /* Keep this        */
    '| strip trailing ~_~',                      /* Remove underscore*/
    '| xlate upper',                             /* Uppercase        */
    '| stem res.',                               /* Hand 2 rexx      */
    '? f:',                                      /* Conn 2 fanout    */
    '| specs w3.1',                              /* Keep this        */
    '| stem col.',                               /* Hand 2 rexx      */
    '? p2:',                                     /* Conn 2 pick      */
    '| pick w1.1 == ~ETK~',                      /* Select this      */
    '| count lines',                             /* Count occurences */
    '| var entkey'                               /* Store as boolean */
Return
 
 
Get_DistList:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Address CMS 'PIPE (NAME CODE_A.EXEC:595 END ?)',
  '< CODE NAMES *',                              /* Read this        */
  '| pick 1.1 \== ~*~ and 1.1 \== ~~',           /* Ignore these     */
  '| join * x40',                                /* Make 1 record    */
  '| split anycase before string ~:NICK.DISTLIST~',   /* Split here       */
  '| split anycase before string ~:USERS.~',     /* and here         */
  '| pick anycase 1.6 == ~:NICK.~ and w-1;-1 == ~:NODE.'node'~',   /* Select this      */
  '| specs fs . f3;* 1',                         /* Adjust           */
  '| specs w1;-2 1',                             /* More adjusting   */
  '| var distlist'                               /* Store as         */
Return
 
 
Error:
/*---------------------------------------------------------------+
| Show the error message                                         |
+---------------------------------------------------------------*/
Parse Arg emsgline
'EMSG 'Copies(' ',6)'>>>> 'emsgline
Return
