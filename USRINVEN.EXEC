/*---------------------------------------------------------------+
| Function : Create CSV for user inventory                       |
|                                                                |
| File     : USRINVEN EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : *        CLUSTER    Input                           |
|            *        DIRMPART   Input                           |
|            <name>   CSV        Output                          |
|                                                                |
| Called   : USRINVEN .                                          |
|                                                                |
| Comments : "* doel:" Needs description of usage                |
|            "ACCOUNT" statement can contain "home" node         |
|            If you get strange results try DIRM BACKUP.         |
|                                                                |
| Note     : ACCOUNT needs to be implemented                     |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20220920 15:16:19                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Source . . myfn myft .
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Address CMS 'VMLINK DIRMAINT 1DF (NON QUIET .fm'
If rc=0 Then Parse Pull  . . wfm .
        Else Exit rc
hdr='Server   ; Memory   ; LPAR ; Status ; CPUs ; Doel ; 'myfn node Date('S') Time()
member.0=0
name.0=0
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
'PIPE (NAME USRINVEN.EXEC:38 END ?)',
   'CP QUERY USERID',                            /* Ask CP           */
   '| specs w3.1',                               /* Keep this        */
   '| var node',                                 /* Store as         */
   '| specs -2;-1',                              /* Reduce to this   */
   '| var shortnode',                            /* Hand 2 rexx      */
   '?',                                          /* 2nd pipe         */
   'cp QUERY SSI',                               /* Ask cp           */
   '| pick anycase w3.1 == ~Joined~',            /* Take these       */
   '| specs w2.1',                               /* Keep this        */
   '| stem member.'                              /* Hand 2 rexx      */
If member.0=0 Then 'PIPE (NAME USRINVEN.EXEC:49 END ?) var node | stem member.'
/*---------------------------------------------------------------+
| Get all the names from the members of the cluster              |
+---------------------------------------------------------------*/
Do m=1 To member.0
   'PIPE (NAME USRINVEN.EXEC:54 END ?)',
      'cp AT 'member.m' CMD QUERY NAMES',        /* Ask cp at lpar   */
      '| split at ,',                            /* Split here       */
      '| strip',                                 /* Remove spaces    */
      '| pick anycase w1.1 \== ~VSM~ and',       /* Ignore           */
                     'w3.1 \== ~TCPIP~',         /* ..these          */
      '| pick anycase 1.3 \== ~SSL~',            /* ...and these     */
      '| pick anycase w3.1 \== ~SSI~',           /* ....and these    */
      '| specs w1.1 1.8',                        /* Adjust layout    */
              '~2 'member.m'~ nw',               /* .                */
              'w3.1 nw',                         /* .                */
      '| change ~DSC~D~',                        /* Replace this     */
      '| if: if pick w4.1 \== ~D~',              /* If not this      */
      '|     specs w1.1 1.8',                    /* .add other       */
                  'w2.1 nw',                     /* ..marker         */
                  'w3.1 nw',                     /* .                */
                  '~*~ nw',                      /* .                */
      '| if:',                                   /* .                */
      '| stem name. append'                      /* Append 2 stem    */
End m
'PIPE stem name. | cons'
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
'PIPE (NAME USRINVEN.EXEC:77 END ?)',
   'cms LISTFILE * CLUSTER 'wfm' (NOH',          /* List these       */
   '| append cms LISTFILE * DIRMPART 'wfm' (NOH', /* .and these       */
   '| getfiles',                                 /* Read them        */
   '| specs 1.72',                               /* Chop of linenos  */
   '| strip',                                    /* Remove spaces    */
   '| fo: fanout',                               /* Duplicate records*/
   '| pick anycase w1.1 == ~USER~ or',           /* Select these     */
                  'w1.1 == ~IDENTITY~',          /* .                */
   '| specs w2.1 1.8 ~1~ nw',                    /* Adjust           */
           'pad _ w4.1 nw.8 right',              /* .                */
   '| append stem name.',                        /* Add Q NAMES      */
   '| fi: faninany',                             /* Take in others   */
   '| sort w1.2',                                /* Sort on id & no  */
   '| join keylen 8',                            /* Join on id       */
   '| if1: if pick w4.1 \== ~2~',                /* If this          */
   '|      specs w1.3 1',                        /* .Add this        */
                '~2 'Copies('_',Length(node))' _~ nw', /* .                */
                'w4;* nw',                       /* .                */
   '| if1:',                                     /* Conn 2 if        */
   '| if2: if pick w7.1 \== ~3~',                /* If this          */
   '|      specs w1.6 1 ~3 _~ nw w7;* nw',       /* .Add this        */
   '| if2:',                                     /* Conn 2 if        */
   '| if3: if pick w9.1 \== ~4~',                /* If this          */
   '|      specs w1.8 1 ~4 _~ nw w9;* nw',       /* .Add this        */
   '| if3:',                                     /* Conn 2 if        */
   '| change w2.1 ~1~;~',                        /* Replace these    */
   '| change w4.1 ~2~;~',                        /* .                */
   '| change w7.1 ~3~;~',                        /* .                */
   '| change w9.1 ~4~;~',                        /* .                */
   '| specs w1.1 1.8',                           /* Adjust layout    */
           'w2.4 nw',                            /* .                */
           '~;~ nw',                             /* .                */
           'w6;* nw',                            /* .                */
           '~;~ nw',                             /* .                */
   '| preface var hdr',                          /* Add header       */
   '| > 'Substr(node'_'myfn,1,8)' CSV A',        /* Send 2 file      */
   '? fo:',                                      /* Conn 2 fanout    */
   '| pick anycase w1.1 == ~USER~ or',           /* Select these     */
                  'w1.1 == ~IDENTITY~',          /* .                */
   '| specs w2.1 1.8 x40 n',                     /* Adjust           */
   '| j1: juxtapose',                            /* Put side by side */
   '| sort count w1.2',                          /* Count occurences */
   '| specs 11.8 1.8 ~3~ nw 1.10 strip nw',      /* Adjust layout    */
   '| fi:',                                      /* Conn 2 faninany  */
   '? fo:',                                      /* Conn 2 fanout    */
   '| pick w1.1 == ~CPU~',                       /* Select these     */
   '| j1:',                                      /* Conn 2 juxtapose */
   '? fo:',                                      /* Conn 2 fanout    */
   '| pick anycase w1.1 == ~USER~ or',           /* Select these     */
                  'w1.1 == ~IDENTITY~',          /* .                */
   '| specs w2.1 1.8 x40 n',                     /* Adjust layout    */
   '| j2: juxtapose',                            /* Put side by side */
   '| specs w1.1 1.8 ~4~ nw w2;* nw',            /* Adjust record    */
   '| fi:',                                      /* Conn 2 faninany  */
   '? fo:',                                      /* Conn 2 fanout    */
   '| change anycase ~*DOEL:~* DOEL:~',
   '| pick anycase w1.2 == ~* DOEL:~',           /* Select these     */
   '| specs w3;* 1',                             /* Keep this        */
   '| j2:'                                       /* Conn 2 juxtapose */
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Address CMS 'VMLINK DIRMAINT 1DF <DET (NON QUIET'
Exit
