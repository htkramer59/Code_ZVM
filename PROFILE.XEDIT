/*--------------------------------------------------------------------+
| Function : Do some settings I like in my XEDIT sessions             |
|                                                                     |
| File     : PROFILE  XEDIT                                           |
+---------------------------------------------------------------------+
| Version  : 1.1.0                                                    |
|                                                                     |
| Files    : .                                                        |
|                                                                     |
| Called   : N/A                                                      |
|                                                                     |
| Comments : .                                                        |
|                                                                     |
| Author   : H.T.Kramer, ICU-IT Almere                                |
| Created  : 20120316 11:16:41                                        |
| Changes  : .                                                        |
|            20210428 H.T.Kramer : Rework of the PROFILE XEDIT        |
|            20191108 H.T.Kramer : Update to activate FMTP from MANY  |
|                                                                     |
+--------------------------------------------------------------------*/
'SET LINEND OFF'
'EXTRACT /RECFM/TRUNC/LRECL/FNAME/FTYPE/FMODE'
/*---------------------------------------------------------------+
| Define the control char and colors we are going to use...      |
+---------------------------------------------------------------*/
ctlchars='% ESCAPE;',
         'Y PROTECT   YELLOW HIGH;y NOPROTECT YELLOW HIGH;',
         'W PROTECT   WHITE  HIGH;w NOPROTECT WHITE  HIGH;',
         'B PROTECT   BLUE   HIGH;b NOPROTECT BLUE   HIGH;',
         'G PROTECT   GREEN  HIGH;g NOPROTECT GREEN  HIGH;',
         'R PROTECT   RED    HIGH;r NOPROTECT RED    HIGH;',
         '$ OFF'
/*--------------------------------------------------------------------+
| Set the synonyms to use the MANY macro                              |
+--------------------------------------------------------------------*/
prefix='BOX COM DCM HD MC TAG PN PIPE IF DO SAV FIL NUL TR Z',
       'PBOX T TS B N CEN LEF RIG UPP LOW ANC UL OL TBL PCM',
       'SEL SUB CONS FMTP PIPNM PN NODE START RXTS RXHLP RXEXT',
       'VSPEC MARK'
/*---------------------------------------------------------------+
| commandline synonymns to use the MANY macro                    |
+---------------------------------------------------------------*/
cmdl='HD MC PIPNM NODE START RXHLP RXEXT RXTS'
/*---------------------------------------------------------------+
| Other synonyms                                                 |
+---------------------------------------------------------------*/
othsyn='F  1 FORWARD;FI 2 FIND;BO 2 BOTTOM;B  1 BACKWARD;SYN E  1 X'
/*---------------------------------------------------------------+
| Settings...                                                    |
+---------------------------------------------------------------*/
settings='LINEND ON #;NONDISP ?;HEX ON;CMDLINE TOP;',
         'CURLINE ON 4;MSGLINE ON 3 1 OVERLAY;',
         'PREFIX NULLS LEFT;COLOR PREFIX BLUE;COLOR FILEAR YELLOW;',
         'VERIFY 1 *;ZONE 1 *;REMOTE ON;NULLS ON;STAY ON;',
         'SPILL WORD;SCALE ON 3;TEXT ON;NUMBER ON;AUTOSAVE 10;CASE MIXED IGNORE'
/*--------------------------------------------------------------------+
| Set my PF keys, using 1 to 12, separated by ;                       |
+--------------------------------------------------------------------*/
pfkeys='HELP; ;ONLY SET MSGMODE OFF#QUIT#SET MSGMODE ON#FILE;SPLTJOIN;RGTLEFT;SCREEN 2;BACKWARD;FORWARD; ;',
       'TOP#NEXT;BOT#BACKWARD#NEXT 2;ONLY QUIT'
pfkeyline='Help;_;File;Sp/Jn;<->;Scr2;Back;Forw;_;Top;Bot;Quit'
/*---------------------------------------------------------------+
| Do the PF key settings                                         |
+---------------------------------------------------------------*/
Address CMS 'PIPE (NAME PROFILE.XEDIT:64 END ?)',
    'var pfkeys',                                /* Take these       */
    '| dup 1',                                   /* Duplicate        */
    '| split at ;',                              /* Split here       */
    '| specs ~SET PF~ 1',                        /* Compose          */
            'pad 0 recno n.2 right',             /* ....setting      */
            'w1;* nw',                           /* .                */
    '| fi: faninany',                            /* Take in others   */
    '| subcom xedit',                            /* Hand 2 xedit     */
    '?',                                         /* 2nd pipe         */
    'var ctlchars',                              /* Take this        */
    '| split at ;',                              /* Split here       */
    '| specs ~SET CTLCHAR~ 1 1;* nw',            /* Compose cmds     */
    '| fi:'                                      /* Conn 2 faninany  */
Address CMS 'PIPE (NAME PROFILE.XEDIT:93 END ?)',
     'var pfkeyline',                            /* Take this        */
     '| split at ;',                             /* Split here       */
     '| specs recno 1.2',                        /* Compose          */
             '~=%G~ n',                          /* ...setting       */
             'w1.1 n',                           /* .                */
             '~%W~ n',                           /* .                */
     '| join *',                                 /* Make 1 record    */
     '| space 0',                                /* Suppress spaces  */
     '| specs ~%W~ 6 1;* n',                     /* Shift 6          */
     '| var pfkresrvd'                           /* Store as         */
/*---------------------------------------------------------------+
| Do the settings...synonyms and others                          |
+---------------------------------------------------------------*/
Address CMS 'PIPE (NAME PROFILE.XEDIT:107 END ?)',
     'var prefix',                               /* Take this        */
     '| split',                                  /* Split in records */
     '| specs ~SET PREFIX SYNONYM~ 1 w1.1 nw ~MANY~ nw',
     '| fi: faninany',                           /* Take in others   */
     '| subcom xedit',                           /* Hand it to XEDIT */
     '?',                                        /* Start 2nd PIPE   */
     'var cmdl',                                 /* Take this        */
     '| split',                                  /* Split in records */
     '| specs ~SET SYNONYM~ 1 w1.1 nw ~MANY~ nw',  /* Compose setting  */
     '| fi:',                                    /* Conn. 2 faninany */
     '?',                                        /* 3rd PIPE         */
     'var settings',                             /* Take this        */
     '| split at ;',                             /* Split here       */
     '| specs ~SET~ 1 1;* nw',                   /* Compose setcmds  */
     '| fi:',                                    /* Conn 2 faninany  */
     '?',                                        /* 4th PIPE         */
     'var othsyn',                               /* Take this        */
     '| split at ;',                             /* Split here       */
     '| specs ~SET SYNONYM~ 1 1;* nw',           /* Compose cmds     */
     '| fi:'                                     /* Conn 2 faninany  */
/*---------------------------------------------------------------+
| Show the line with PF keys                                     |
+---------------------------------------------------------------*/
'SET RESERVED  -1 HIGH 'pfkresrvd
Exit
