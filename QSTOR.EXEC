/*---------------------------------------------------------------+
| Function : Show memory/storage usage                           |
|                                                                |
| File     : QSTOR    EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called   : QSTOR    .                                          |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, At_home,                                |
| Created  : 20250815 15:47:23                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg . '(' sortcolin sortdir .
Parse Source . . myfn myft .
'VMFCLEAR'
Address CMS 'VMLINK WORK (QUIET .fm'
If rc=0 Then Parse Pull . . ws .
        Else ws='A'
/*---------------------------------------------------------------+
| Select the columns to sort on                                  |
+---------------------------------------------------------------*/
Select
When sortcolin='' |,
     sortcolin='NAME'    Then sortcol='| sort w1.1'
When sortcolin='STATUS'  Then sortcol='| sort w2.1'
When sortcolin='IPL'     Then sortcol='| sort w3.1'
When sortcolin='STORAGE' Then sortcol='| specs w1.3 1 pad 0  w4.1 nw.5 right pad space w5;* nw.78 right | sort w4.1'
When sortcolin='PAGES'   Then sortcol='| specs w1.5 1 pad 0  w6.1 nw.5 right pad space w7;* nw.66 right | sort w6.1'
When sortcolin='WS'      Then sortcol='| sort w8.1'
When sortcolin='RDR'     Then sortcol='| sort w10.1'
When sortcolin='PRT'     Then sortcol='| sort w11.1'
When sortcolin='PCH'     Then sortcol='| sort w12.1'
When sortcolin='IO'      Then sortcol='| sort w13.1'
When sortcolin='PERC'    Then sortcol='| sort w14.1'
Otherwise Call Exit '81 Invalid sortcolumn: 'sortcolin
End
/*---------------------------------------------------------------+
| Set the direction for the sort Ascending or Descending         |
+---------------------------------------------------------------*/
Select
When sortdir=''  |,
     sortdir='D' Then sortdir='D'
When sortdir='A' Then Nop
Otherwise Call Exit '82 Invalid sort direction: 'sordir
End
/*---------------------------------------------------------------+
| Ask cp for the names and info                                  |
+---------------------------------------------------------------*/
'PIPE (NAME QSTOR.EXEC:55 END ?)',
  'cp QUERY NAMES',                              /* Ask CP           */
  '| split at ,',                                /* Split here       */
  '| strip',                                     /* Remove spaces    */
  '| pick w1.1 \== ~VSM~',                       /* Ignore this one  */
  '| sort fs - f1',                              /* Sort on name     */
  '| f: fanout',                                 /* Dupl. records    */
  '| specs fs - f1 1.8',                         /* Keep this        */
  '| stem id.',                                  /* Hand 2 rexx      */
  '? f:',                                        /* Conn. 2 fanout   */
  '| specs fs - f2 1.10',                        /* Keep this        */
  '| stem stat.',                                /* Hand 2 rexx      */
  '?',                                           /* 2nd pipe         */
  'cp QUERY STORAGE',                            /* Ask CP           */
  '| specs w3.1 1',                              /* Keep this        */
  '| var size',                                  /* Hand 2 rexx      */
  '| siconv w1.1 g2k 8.0',                       /* Convert 2 kb     */
  '| var realstor'                               /* Hand 2 rexx      */
/*---------------------------------------------------------------+
| Loop thru the user ids found                                   |
+---------------------------------------------------------------*/
Do i=1 To id.0
   'PIPE (NAME QSTOR.EXEC:77 END ?)',
       'cp INDICATE USER 'id.i' EXPANDED',       /* Ask CP           */
       '| strip',                                /* Remove spaces    */
       '| pick anycase substr 1.7 of w1.1 == ~IPLSYS=~ or',   /* Keep these       */
                      'w1.1 == ~DEFINED~ or',                 /* .                */
                      'substr 1.5 of w2.1 ==  ~MAIN=~ or',    /* .                */
                      'substr 1.3 of w1.1 == ~WS=~ or',       /* .                */
                      'substr 1.4 of w1.1 == ~RDR=~',         /* .                */
       '| join * x40',                           /* Make 1 record    */
       '| change ~DEV ~~',                       /* Suppress the     */
       '| change anycase ~IPLSYS=~~',            /* .following       */
       '| change anycase ~XSTORE=---~~',         /* .                */
       '| change anycase ~PAGES:~~',             /* .                */
       '| change anycase ~SIZE=~~',              /* .                */
       '| change anycase ~LIMIT=~~',             /* .                */
       '| change anycase ~MAIN=~~',              /* .                */
       '| change anycase ~DASD=~~',              /* .                */
       '| change anycase ~RESERVED=~~',          /* .                */
       '| change anycase ~WS=~~',                /* .                */
       '| change anycase ~RDR=~~',               /* .                */
       '| change anycase ~PRT=~~',               /* .                */
       '| change anycase ~PCH=~~',               /* .                */
       '| change anycase ~IO=~~',                /* .                */
       '| specs w1.1 1.10 w4.1 nw right w6;* nw',   /* Adjust layout    */
       '| specs ~'id.i stat.i'~ 1 1;* nw',       /* Add id & status  */
       '| var temp'                              /* Hand 2 rexx      */
   id.i=temp                                     /* Move into stem   */
End i
/*---------------------------------------------------------------+
| Work on the results                                            |
+---------------------------------------------------------------*/
numeric digits 64
'PIPE (NAME QSTOR.EXEC:109 END ?)',
   'stem id.',                                   /* Take these       */
   '| siconv w4.1 g2m 6.0 M',                    /* Convert these    */
   '| siconv w5.1 g2m 6.0 M',                    /* .to mb           */
   '| specs',                                    /* Adjust           */
           'w1.1 1.8',                           /* .layout          */
           'w2.1 nw.8',                          /* .                */
           'w3.1 nw.4 right',                    /* .                */
           'w4.1 nw.5 right',                    /* .                */
           'w5.1 nw.5 right',                    /* .                */
           'w6.1 nw.6 right',                    /* .                */
           'w7.1 nw.6 right',                    /* .                */
           'w8.1 nw.6 right',                    /* .                */
           'w9.1 nw.6 right',                    /* .                */
           'w12.1 nw.8 right',                   /* .                */
           'w13.1 nw.8 right',                   /* .                */
           'w14.1 nw.8 right',                   /* .                */
           'w15.1 nw.8 right',                   /* .                */
   '| buffer',                                   /* Prevent stall    */
   '| spec 1;* 1',                               /* Convert this     */
          'a: w8.1 .',                           /* .to kb           */
          'set #0:=a*4',                         /* .                */
          'print #0 nw',                         /* .                */
   '| specs 1;* 1',                              /* Calculate        */
           'a: w-1;-1 .',                        /* .percentage      */
           'set #0:=(a/'realstor')*100',         /* ..memory         */
           'print #0 pict --9.99 nw.6',          /* ...used          */
           '~%~ n',                              /* .                */
   '| specs 1.99 1 w-1;-1 nw.7 right',           /* Adjust layout    */
   sortcol sortdir,                              /* Do the sorting   */
   '| specs 1;* 1',                              /* Calculate        */
           'a: w8.1 .',                          /* .total           */
           'set #0+=a',                          /* ..memory         */
           'eof',                                /* ...used          */
           '~Total:~ 38',                        /* .                */
           'print #0 nw',                        /* .                */
   '| d: drop last 1',                           /* Move this away   */
   '| append  literal ------   ------   ---  ----- ----- ------ ------ ------ ------ ------   ------   ------   -------- --------',
   '| fi: fanin',                                /* Take in last rec */
   '| preface literal ------   ------   ---  ----- ----- ------ ------ ------ ------ ------   ------   ------   -------- --------',
   '| preface literal Userid   Status   IPL  Storg Storg in MEM on DSD Set Sz Pages  in RDR   in PRT   in PCH   Requests MEMory',
   '| preface literal                        Deflt &DCSS Pages  Pages  Workng Resrvd Cards    Lines    Cards    IO       Perc. ',
   '| > 'myfn' DETAILS 'ws,                      /* Write 2 file     */
   '? d:',                                       /* Conn 2 drop      */
   '| specs 1;* 1',                              /* Summarize        */
           'a: w2.1 .',                          /* .                */
           'set #0:=(a/'realstor')*100',         /* .                */
           '~=~ n',                              /* .                */
           'print #0 pict --9.99 n',             /* .                */
           '~% of MEMory~ n',                    /* .                */
   '| fi:'                                       /* Conn 2 fanin     */
/*--------------------------------------------------------------------+
| Show the content                                                    |
+--------------------------------------------------------------------*/
Address CMS 'XEDIT 'myfn' DETAILS 'ws
/*--------------------------------------------------------------------+
| Clean up and remove the workspace (if present)                      |
+--------------------------------------------------------------------*/
Address CMS 'ERASE 'myfn' DETAILS 'ws
If ws<>'A' Then Address CMS 'VMLINK WORK <DET (QUIET'
 
Exit:
/*---------------------------------------------------------------+
| General Exit routine                                           |
+---------------------------------------------------------------*/
Parse Arg exrc errmsg
If Datatype(exrc,'NUM') Then Say errmsg exrc
                        Else exrc=0
Exit exrc
 
