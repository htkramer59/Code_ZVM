/*---------------------------------------------------------------+
| Function : Show memory/storage usage                           |
|                                                                |
| File     : QSTOR    EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called   : QSTOR    .                                          |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, At_home,                                |
| Created  : 20250815 15:47:23                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg . '(' sortcolin sortdir .
Parse Source . . myfn myft .
'VMFCLEAR'
Address CMS 'VMLINK WORK (QUIET .fm'
If rc=0 Then Parse Pull . . ws .
        Else ws='A'
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Select
When sortcolin='' |,
     sortcolin='NAME'    Then sortcol='| sort w1.1'
When sortcolin='STATUS'  Then sortcol='| sort w2.1'
When sortcolin='IPL'     Then sortcol='| sort w3.1'
When sortcolin='STORAGE' Then sortcol='| specs w1.3 1 pad 0  w4.1 nw.5 right pad space w5;* nw.78 right | sort w4.1'
When sortcolin='PAGES'   Then sortcol='| specs w1.5 1 pad 0  w6.1 nw.5 right pad space w7;* nw.66 right | sort w6.1'
When sortcolin='WS'      Then sortcol='| sort w8.1'
When sortcolin='RDR'     Then sortcol='| sort w10.1'
When sortcolin='PRT'     Then sortcol='| sort w11.1'
When sortcolin='PCH'     Then sortcol='| sort w12.1'
When sortcolin='IO'      Then sortcol='| sort w13.1'
When sortcolin='PERC'    Then sortcol='| sort w14.1'
Otherwise Call Exit '81 Invalid sortcolumn: 'sortcolin
End
/*---------------------------------------------------------------+
|                                                                |
+---------------------------------------------------------------*/
Select
When sortdir=''  |,
     sortdir='D' Then sortdir='D'
When sortdir='A' Then Nop
Otherwise Call Exit '82 Invalid sort direction: 'sordir
End
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
'PIPE (NAME QSTOR.EXEC:55 END ?)',
  'cp QUERY NAMES',                              /* .                */
  '| split at ,',                                /* .                */
  '| strip',                                     /* .                */
  '| pick w1.1 \== ~VSM~',                       /* .                */
  '| sort fs - f1',                              /* .                */
  '| f: fanout',                                 /* .                */
  '| specs fs - f1 1.8',                         /* .                */
  '| stem id.',                                  /* .                */
  '? f:',                                        /* .                */
  '| specs fs - f2 1.10',                        /* .                */
  '| stem stat.',                                /* .                */
  '?',                                           /* .                */
  'cp QUERY STORAGE',                            /* .                */
  '| specs w3.1 1',                              /* .                */
  '| var size',
  '| siconv w1.1 g2k 8.0',
  '| var realstor'                               /* .                */
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Do i=1 To id.0
   'PIPE (NAME QSTOR.EXEC:77 END ?)',
       'cp INDICATE USER 'id.i' EXPANDED',       /* .                */
       '| strip',                                /* .                */
       '| pick anycase substr 1.7 of w1.1 == ~IPLSYS=~ or',   /* .                */
                      'w1.1 == ~DEFINED~ or',                 /* .                */
                      'substr 1.5 of w2.1 ==  ~MAIN=~ or',    /* .                */
                      'substr 1.3 of w1.1 == ~WS=~ or',       /* .                */
                      'substr 1.4 of w1.1 == ~RDR=~',         /* .                */
       '| join * x40',                           /* .                */
       '| change ~DEV ~~',                       /* .                */
       '| change anycase ~IPLSYS=~~',            /* .                */
       '| change anycase ~XSTORE=---~~',         /* .                */
       '| change anycase ~PAGES:~~',             /* .                */
       '| change anycase ~SIZE=~~',              /* .                */
       '| change anycase ~LIMIT=~~',             /* .                */
       '| change anycase ~MAIN=~~',              /* .                */
       '| change anycase ~DASD=~~',              /* .                */
       '| change anycase ~RESERVED=~~',          /* .                */
       '| change anycase ~WS=~~',                /* .                */
       '| change anycase ~RDR=~~',               /* .                */
       '| change anycase ~PRT=~~',               /* .                */
       '| change anycase ~PCH=~~',               /* .                */
       '| change anycase ~IO=~~',                /* .                */
       '| specs w1.1 1.10 w4.1 nw right w6;* nw',   /* .                */
       '| specs ~'id.i stat.i'~ 1 1;* nw',       /* .                */
       '| var temp'                              /* .                */
   id.i=temp
End i
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
numeric digits 64
'PIPE (NAME QSTOR.EXEC:109 END ?)',
   'stem id.',                                   /* .                */
   '| siconv w4.1 g2m 6.0 M',                    /* .                */
   '| siconv w5.1 g2m 6.0 M',                    /* .                */
   '| specs',                                    /* .                */
           'w1.1 1.8',                           /* .                */
           'w2.1 nw.8',                          /* .                */
           'w3.1 nw.4 right',                    /* .                */
           'w4.1 nw.5 right',                    /* .                */
           'w5.1 nw.5 right',                    /* .                */
           'w6.1 nw.6 right',                    /* .                */
           'w7.1 nw.6 right',                    /* .                */
           'w8.1 nw.6 right',                    /* .                */
           'w9.1 nw.6 right',                    /* .                */
           'w12.1 nw.8 right',                   /* .                */
           'w13.1 nw.8 right',                   /* .                */
           'w14.1 nw.8 right',                   /* .                */
           'w15.1 nw.8 right',                   /* .                */
   '| buffer',
   '| spec 1;* 1',                               /* .                */
          'a: w8.1 .',                           /* .                */
          'set #0:=a*4',                         /* .                */
          'print #0 nw',                         /* .                */
   '| specs 1;* 1',                              /* .                */
           'a: w-1;-1 .',                        /* .                */
           'set #0:=(a/'realstor')*100',         /* .                */
           'print #0 pict --9.99 nw.6',          /* .                */
           '~%~ n',                              /* .                */
   '| specs 1.99 1 w-1;-1 nw.7 right',           /* .                */
   sortcol sortdir,
   '| specs 1;* 1',                              /* .                */
           'a: w8.1 .',                          /* .                */
           'set #0+=a',                          /* .                */
           'eof',                                /* .                */
           '~Total:~ 38',                        /* .                */
           'print #0 nw',                        /* .                */
   '| d: drop last 1',                           /* .                */
   '| append  literal ------   ------   ---  ----- ----- ------ ------ ------ ------ ------   ------   ------   -------- --------',
   '| fi: fanin',                                /* .                */
   '| preface literal ------   ------   ---  ----- ----- ------ ------ ------ ------ ------   ------   ------   -------- --------',
   '| preface literal Userid   Status   IPL  Storg Storg in MEM on DSD Set Sz Pages  in RDR   in PRT   in PCH   Requests MEMory',
   '| preface literal                        Deflt &DCSS Pages  Pages  Workng Resrvd Cards    Lines    Cards    IO       Perc. ',
   '| > 'myfn' DETAILS 'ws,                      /* .                */
   '? d:',                                       /* .                */
   '| specs 1;* 1',                              /* .                */
           'a: w2.1 .',                          /* .                */
           'set #0:=(a/'realstor')*100',         /* .                */
           '~=~ n',                              /* .                */
           'print #0 pict --9.99 n',             /* .                */
           '~% of MEMory~ n',                    /* .                */
   '| fi:'                                       /* .                */
 
Address CMS 'XEDIT 'myfn' DETAILS 'ws
Address CMS 'ERASE 'myfn' DETAILS 'ws
If ws<>'A' Then Address CMS 'VMLINK WORK <DET (QUIET'
 
Exit:
/*---------------------------------------------------------------+
| General Exit routine                                           |
+---------------------------------------------------------------*/
Parse Arg exrc errmsg
If Datatype(exrc,'NUM') Then Say errmsg exrc
                        Else exrc=0
Exit exrc
 
