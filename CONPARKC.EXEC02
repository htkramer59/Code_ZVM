/*---------------------------------------------------------------+
| Function : Show the log files to the client                    |
|                                                                |
| File     : CONPARKC EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : CONPARK  NAMES                                      |
|            VMLINK   EXEC                                       |
|            LISTDIR  EXEC                                       |
|            FILELIST EXEC                                       |
|                                                                |
| Called   : CONPARKC .                                          |
|                                                                |
| Comments : Arguments are described in the bootstrap (CONPARK)  |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20180618 11:35:20                                   |
| Changes  : .                                                   |
|            20241209 H.T.Kramer : Added some details when       |
|                                  invoked w/o options/input     |
|            20200403 H.T.Kramer : Made wildcards work and extra |
|                                  simplification of the code    |
|            20190502 H.T.Kramer : Simplified                    |
|                                                                |
+---------------------------------------------------------------*/
Parse Source . .  myfn .
'PIPE (NAME CONPARKC.EXEC:28 END ?)',
    'rexxvars 2 toload',                        /* Get vars from caller */
    '| pick fs / f2 == ~LOGID~',                /* Need this       */
    '| varload'                                 /* Give to rexx    */
/*---------------------------------------------------------------+
| Act according to content of logid                              |
+---------------------------------------------------------------*/
Address CMS 'VMLINK 'myfn' (ONLY(CONPARK) NOTYPE NOINVOKE .pr'
If rc=0 Then Parse Pull . . . dirl .
        Else Do
        Say 'CONPARK details not found'
        Exit 28
        End
If Pos('*',logid)>0 Then Call ShowMultiDirlist
                    Else Do
                         If logid='' Then Call ShowDirlist
                                     Else Call ShowContent
                         End
/*---------------------------------------------------------------+
| End of show                                                    |
+---------------------------------------------------------------*/
Address CMS 'VMLINK 'myfn' <DET (ONLY(CONPARK) NOTYPE'
If exrc='' | exrc='EXRC' Then exrc=0
Exit exrc
 
 
ShowMultiDirlist:
/*---------------------------------------------------------------+
| Show multiple dirlists                                         |
+---------------------------------------------------------------*/
'MAKEBUF'
bufno=rc
'PIPE (NAME CONPARKC.EXEC:60 END ?)',
   'cms LISTDIR 'dirl,                          /* Get all dirs     */
   '| wildcard fs . f-1;-1 /'logid'/',          /* Select these     */
   '| specs w2.1',                              /* Keep this        */
   '| t: take first 1',                         /* Take 1           */
   '| var firstdir',                            /* Store as         */
   '? t:',                                      /* Conn 2 take      */
   '| specs ~DIRLIST~ 1 w1.1 nw ~(APPEND~ nw',  /* Prep cmds        */
   '| append literal SET RESERVED 2 WHITE NON NOH Found IDs: 'logid,
   '| stack'                                    /* Stack m          */
Address CMS 'DIRLIST 'firstdir
'DROPBUF 'bufno
Return
 
 
ShowDirlist:
/*---------------------------------------------------------------+
| Show all the users that PARK console files                     |
+---------------------------------------------------------------*/
'PIPE (NAME CONPARKC.EXEC:79 END ?)',
   'cms LISTDIR 'dirl,
   '| drop first 2',
   '| specs fs . ~COVERLAY User:~ 1 f-1;-1 nw.10',
   '| stem name.'
Queue 'SET RESERVED 2 WHITE NON NOH Found IDs: ALL'
Queue '+1'
Queue 'CLOCATE :40'
Do n=1 To name.0
  Queue name.n
  Queue '+1'
End n
Queue ':2'
Address CMS 'DIRLIST 'dirl
Return
 
 
ShowContent:
/*---------------------------------------------------------------+
| Show the content of an id                                      |
+---------------------------------------------------------------*/
'PIPE (NAME CONPARKC.EXEC:100 END ?)',
   'cms LISTDIR 'dirl,                          /* Ask cms          */
   '| specs w2.1',                              /* Keep this        */
   '| pick fs . f-1;-1 == ~'logid'~',           /* Select these     */
   '| var fl_inp',                              /* Store as         */
   '| count lines',                             /* How many         */
   '| var fnd'                                  /* Store as         */
If fnd Then Do
       Address cms 'VMLINK .DIR 'fl_inp' <* A-Z (NONAMES QUIET FILELIST'
       End
       Else Do
       Say 'Id not present in CONPARK: 'logid
       exrc=rc
       End
Return
