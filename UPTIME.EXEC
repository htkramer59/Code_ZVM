/*---------------------------------------------------------------+
| Function : Calculate the uptime                                |
|                                                                |
| File     : UPTIME   EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called   : UPTIME   .                                          |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, At_home,                                |
| Created  : 20250620 14:42:08                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
'VMFCLEAR'
'PIPE (NAME UPT.EXEC:20 END ?)',
   'cp QUERY CPLEVEL ISODATE',                   /* Ask cp           */
   '| take last 1',                              /* Keep this        */
   '| specs w3.2 1',                             /* Reduce to this   */
   '| specs ~IPLtime~ 1 w1.2 nw',                /* Add label        */
   '| fi: fanin',                                /* Take in CURtime  */
   '| specs w1.3 1 w2.1 nw w3.1 nw',             /* Adjust records   */
   '| dateconv w4.1 iso rb',                     /* Convert dates    */
   '| specs w1.4 1',                             /* Convert time     */
           'a: substr 1.2 of w5.1 .',            /* .to              */
           'b: substr 4.2 of w5.1 .',            /* .  seconds       */
           'c: substr 7.2 of w5.1 .',            /* .                */
           'set #0:=(a*3600)+(b*60)+c',          /* .                */
           'print #0 nw',                        /* .                */
   '| join * x40',                               /* Make 1 record    */
   '| var datestimes',                                /* Hand 2 rexx      */
   '?',                                          /* 2nd pipe         */
   'literal ',                                   /* Empty record     */
   '| timestamp isodate',                        /* Current time     */
   '| specs ~CURtime~ 1 w1.2 nw',                /* Add label        */
   '| fi:'                                       /* Conn 2 fanin     */
/*---------------------------------------------------------------+
| Split the findings into usable items                           |
+---------------------------------------------------------------*/
ipldays=Word(datestimes,4)
ipltime=Word(datestimes,5)
curdays=Word(datestimes,9)
curtime=Word(datestimes,10)
days=Right(curdays-ipldays,4)
time_left=curtime-ipltime
/*---------------------------------------------------------------+
| If the no of seconds is below 0 subtract 1 day and add at other|
+---------------------------------------------------------------*/
If time_left<=0 Then Do
                days=days-1
                time_left=time_left+(24*3600)
                End
/*---------------------------------------------------------------+
| Rework to hh:mm:ss                                             |
+---------------------------------------------------------------*/
hours=Right(time_left%3600,2,'0')
time_left=time_left//3600
mins=Right(time_left%60,2,'0')
secs=Right(time_left//60,2,'0')
/*---------------------------------------------------------------+
| Display the outcome                                            |
+---------------------------------------------------------------*/
Say Subword(datestimes,1,3)
Say Subword(datestimes,6,3)
Say
Say '           Days hh:mm:ss'
Say 'Uptime is: 'days hours':'mins':'secs
