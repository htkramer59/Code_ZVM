/*---------------------------------------------------------------+
| Function : Generic status exec                                 |
|                                                                |
| File     : HOBVM    EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called   : HOBVM    .                                          |
|                                                                |
| Comments : This can be used as a template for other tests.     |
|            Also used to ENABLE/DISABLE messages to the         |
|            XYMON server when the client starts or stops.       |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20211223 09:54:26                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg verb test msg '(' files ')'
Parse Value verb With verb '+' ttl .
parse source . mytype myfn myft .
/*---------------------------------------------------------------+
| Collect values from our caller                                 |
+---------------------------------------------------------------*/
'PIPE (NAME HOBVM.EXEC:27 END ?)',
    'rexxvars 1 toload',
    '| varload'
Call Init_Open_Socket
Call Collect_Info          /*  Execute the test                */
Call Send status           /*  Send the result in status to bb */
Call close_term_socket     /*  Close and terminate the socket  */
Exit
 
Collect_Info:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
If test='ALL' Then test='*'
If ttl=''     Then ttl= 30
 
If verb = 'DISABLE' Then Do
   parse value msg with hours msg
   msg = hours*60 msg
   end
If verb = 'ENABLE' Then Do
   msg = ''
   end
If verb = 'STATUS' Then Do
   parse value msg with color msg
   verb = verb||'+'||ttl
   msg = color TimeStamp('TZ') msg
   end
status = verb machine||'.'||test msg
Say status
/*---------------------------------------------------------------+
| Prepare all before sending                                     |
+---------------------------------------------------------------*/
If Words(files)<>0 Then status = status '15'x
'PIPE (NAME HOBVM.EXEC:61 END ?)',
  'var files',                                   /* Take this        */
  '| split',                                     /* Split            */
  '| join 2 x40',                                /* Make sets of 3wrd*/
  '| getfiles',                                  /* Read them        */
  '| strip trailing',                            /* Remove spaces    */
  '| preface var status',                        /* Add old one      */
  '| join * x15',                                /* Join with cr     */
  '| specs 1;* 1 x15 n',                         /* Add final cr     */
  '| var status'                                 /* Hand 2 rexx      */
Return
 
 
DoSock:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Parse Arg sockinput
ret=Socket(sockinput)
parse var ret rc .
If rc<>0 Then Say TimeStamp()' : 'sockinput' rc=' ret
Return
 
 
Init_Open_Socket:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DoSock 'Initialize,hobbitvm'
Call DoSock 'Socket'
Parse Var ret rc sid .
Call DoSock 'Setsockopt,'sid',SOL_SOCKET,SO_ASCII,On'
name='AF_INET' bbport bbdisplay
Call DoSock 'Connect,'sid','name
Call DoSock 'Gethostname'
Parse Var ret . hostname .
Call DoSock 'Getdomainname'
Parse Var ret . Domainame .
fullname=hostname||'.'||Domainame
Return
 
 
Close_Term_Socket:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DoSock 'Close,'sid
Call DoSock 'Terminate'
Return
 
 
Send:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
parse arg out_data
Call DoSock 'Send,'sid','out_data
Return
 
 
TimeStamp:
/*---------------------------------------------------------------+
| Return a timestamp and with TZ switch a linux like one         |
+---------------------------------------------------------------*/
Parse Upper Arg sw .
If sw='TZ' Then Do
   now=Date('S')
   mm=Substr(Date('M'),1,3)
   Parse Var now yyyy +4 . +2 dd +2 .
   'PIPE (NAME HOBVM.EXEC:130 END ?)',
      'cp QUERY TIMEZONE',
      '| pick anycase w-1;-1 == ~ACTIVE~',
      '| specs w1.1',
      '| var tz'
   timest=Substr(Date('W'),1,3) mm dd time() tz yyyy
   End
   Else timest=Date('S') Time() myfn
Return timest
