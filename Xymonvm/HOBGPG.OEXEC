/* Xymon Client V1.3 for VM/ESA and z/VM
    Copyright (C) 2003-2010 Richard Smrcina
    This program is free software; you can redistribute it and/or
    modify it under the terms of the GNU General Public License as
    published by the Free Software Foundation; either version 2 of
    the License, or (at your option) any later version.
 
    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
    See the GNU General Public License for more details.
 
    http://www.gnu.org/licenses/old-licenses/gpl-2.0.html            */
/*
/-  Herald ten Dam 04/11/2020                                        -/
/-     Guest Paging                                                  */
trace off
parse source . call_type myname .
cr='15'x                                         /*  Carriage return   */
emsgstart=date('U') time() myname':'   /* Start of error message line  */
'globalv stack BBDISPLAY BBPORT FQDN VMPTK PTKSVM'
pull bbdisplay
pull bbport
pull fqdn
pull vmptk
pull ptksvm
if vmptk <> 1 then exit 4
rc = diagrc('8','QUERY USER' ptksvm)
if word(rc,1) <> 0 then notloggedon=1
call init_open_socket      /*  Initialize and Open the socket  */
if fqdn='Y' then do
  'globalv get fullname'
  machine=fullname
  end
else do
  'globalv get hostname'
  machine=hostname
  end
call get_timestamp         /*  Get a linux-like timestamp      */
call get_cplevel           /*  Get the CP Level for display    */
call do_the_test           /*  Execute the test                */
call send status           /*  Send the result in status to bb write */
call close_term_socket
exit
 
do_the_test:
/*  Check logged on flag, if true send yellow icon with message.  */
if notloggedon=1 then do
  msg = ptksvm 'Not logged on'
  msg = 'yellow' dow month day time() tz year msg
  status = 'status' machine||'.guestpaging' msg cr
  return
end
 
'pipe vmc 'ptksvm' cms erase page listing a  | hole'
'cp sleep 1 sec'
'PIPE vmc 'ptksvm' print UPAGE (SORT DASD PAGESIZE OFF tofile page listing a close | hole'
'reacc quiet'
'cp sleep 1 sec'
/* 'listfile page listing * ' */
'state page listing * '
If rc<>0 Then Exit rc
'PIPE (END ?)',
   '< page listing ',
   '| drop 12 ',
   '| nlocate /                                     /',
   '| siconv B W17.1',
   '| specs ',
   '     a: w1.1 . ',
   '     b: w17.1 . ',
   '     set #0:=a',
   '     set #1:=b*4*1024',
   '    print #0 nextword ',
   '    print #1 nextword ',
   '| specs w1 nw /:/ n PAD 0 W2 NW.20 RIGHT',
   '| specs w1.1 nw.10 w2.1 nw.10 right',
   '| join * x15',
   '| var gpg'
 
msg = green dow month day time() tz year
msg = msg 'Guest paging in bytes'cr||gpg cr
status = 'status' machine||'.guestpaging' msg
return
 
init_open_socket:
ret=Socket('Initialize','SOCGPG')
parse var ret rc .
if rc<>0 then
  say emsgstart 'Initialize rc=' ret
ret=Socket('Socket')
parse var ret rc .
if rc<>0 then
  say emsgstart 'Socket rc=' ret
parse var ret rc sid .
ret=Socket('Setsockopt',sid,'SOL_SOCKET','SO_ASCII','On')
parse var ret rc .
if rc<>0 then
  say emsgstart 'Setsockopt rc=' ret
name='AF_INET' bbport bbdisplay
ret=Socket('Connect',sid,name)
parse var ret rc .
if rc<>0 then
  say emsgstart 'Connect rc=' ret
ret=socket('Gethostname')
parse var ret rc hostname .
if rc<>0 then
  say 'Gethostname:' ret
ret=socket('Getdomainname')
parse var ret rc domainame .
if rc<>0 then
  say 'Getdomainname:' ret
/* fullname=hostname||'.'||domainame */
 
/*  Change periods to commas  */
/* fullname=translate(fullname,',','.') */
/* 'globalv put hostname' */
/* 'globalv put domainame' */
/* 'globalv put fullname' */
bbdate=date('U')
'globalv put bbdate'
return
 
close_term_socket:
ret=Socket('Close',sid)
parse var ret rc .
if rc<>0 then
  say emsgstart 'Close rc=' ret
ret=Socket('Terminate')
parse var ret rc .
if rc<>0 then
  say emsgstart 'Terminate rc=' ret
return
 
send:
parse arg out_data
ret=Socket('Send',sid,out_data)
parse var ret rc .
if rc<>0 then
  say emsgstart 'Send rc=' ret
return
 
get_cplevel:
'execio * CP (stem' CPLEVEL. 'string Q CPLEVEL'
return
 
get_timestamp:
'PIPE CP QUERY TIMEZONE' ,
  '| locate /Active/' ,
  '| specs w1 1' ,
  '| var TZ'
dow = substr(date('W'),1,3)
parse value date('N') with day month year .
timestamp = dow month day time() tz year
return
