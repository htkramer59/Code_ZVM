/*---------------------------------------------------------------+
| Function : Display guest paging                                |
|                                                                |
| File     : HOBGPG   EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called   : HOBGPG   .                                          |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20220111 14:16:26                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg arg1 arg2 . '(' dryrun .
Parse Source . . myfn myft .
If dryrun<>'' Then test='1'
              Else test='0'
/*---------------------------------------------------------------+
| Get the variables from our caller                              |
+---------------------------------------------------------------*/
'PIPE (NAME HOBGPG.EXEC:27 END ?)',
   'rexxvars 1 toload',                          /* Take from caller */
   '| varload'                                   /* Hand 2 rexx      */
 
If \test Then Call Init_Open_Socket      /*  Initialize and Open the socket  */
Call Collect_Info          /*  Execute the test                */
If \test Then Do
              Call Send status           /*  Send the result in status to bb write */
              Call Close_Term_Socket
              End
         Else Do
              'PIPE (NAME HOBGPG.EXEC:38 END ?)',
                 'var status',
                 '| split at x15',
                 '| strip',
                 '| cons'
              End
Exit
 
 
Collect_Info:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
'PIPE (NAME HOBGPG.EXEC:51 END ?)',
   'fcx upage (sort dasd',
   '| frtarget locate anycase ~>System<~',
   '| drop first 1',
   '| specs w1.1 1 ~:~ n',
        'a: w17.1 . ',
        'set #0:=a*4*1024',
        'print #0 pict 9999999999 nextword ',
   '| specs w1.1 1.10 w2.1 nw',
   '| join * x15',
   '| var gpg'
status = 'status' machine||'.guestpaging' green TimeStamp('TZ'),
         'Guest paging in bytes'cr||gpg cr
return
 
 
DoSock:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Parse Arg sockinput
ret=Socket(sockinput)
parse var ret rc .
If rc<>0 Then Say emsgstart sockinput' rc=' ret
Return
 
 
Init_Open_Socket:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DoSock 'Initialize,SOCGPG'
Call DoSock 'Socket'
Parse Var ret rc sid .
Call DoSock 'Setsockopt,'sid',SOL_SOCKET,SO_ASCII,On'
name='AF_INET' bbport bbdisplay
Call DoSock 'Connect,'sid','name
Call DoSock 'Gethostname'
Parse Var ret . hostname .
Call DoSock 'Getdomainname'
Parse Var ret . Domainame .
fullname=hostname||'.'||domainame
If fqdn='Y' Then machine=fullname
            Else machine=hostname
Return
 
 
Close_Term_Socket:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DoSock 'Close,'sid
Call DoSock 'Terminate'
Return
 
 
Send:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
parse arg out_data
Call DoSock 'SEND,'sid','out_data
Return
 
 
TimeStamp:
/*---------------------------------------------------------------+
| Return a timestamp and with TZ switch a linux like one         |
+---------------------------------------------------------------*/
Parse Upper Arg sw .
If sw='TZ' Then Do
   now=Date('S')
   mm=Substr(Date('M'),1,3)
   Parse Var now yyyy +4 . +2 dd +2 .
   'PIPE (NAME HOBGPG.EXEC:125 END ?)',
      'cp QUERY TIMEZONE',
      '| pick anycase w-1;-1 == ~ACTIVE~',
      '| specs w1.1',
      '| var tz'
   timest=Substr(Date('W'),1,3) mm dd time() tz yyyy
   End
   Else timest=Date('S') Time() myfn
Return timest
