/* Xymon Client V1.3 for VM/ESA and z/VM
    Copyright (C) 2003-2010 Richard Smrcina
    This program is free software; you can redistribute it and/or
    modify it under the terms of the GNU General Public License as
    published by the Free Software Foundation; either version 2 of
    the License, or (at your option) any later version.
 
    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
    See the GNU General Public License for more details.
 
    http://www.gnu.org/licenses/old-licenses/gpl-2.0.html            */
/*
/-  Herald ten Dam 09/11/2019                                          -/
/-    Show osa ports for network                                       */
trace off
parse source . call_type myname .
cr='15'x                                         /*  Carriage return   */
emsgstart=date('U') time() myname':'   /* Start of error message line  */
'globalv stack BBDISPLAY BBPORT FQDN PTKSVM'
pull bbdisplay
pull bbport
pull fqdn
pull ptksvm
call init_open_socket      /*  Initialize and Open the socket  */
if fqdn='Y' then do
  'globalv get fullname'
  machine=fullname
  end
else do
  'globalv get hostname'
  machine=hostname
  end
rc = diagrc('8','QUERY USER' ptksvm)
if word(rc,1) <> 0 then notloggedon=1
call get_timestamp         /*  Get a linux-like timestamp      */
call get_cplevel           /*  Get the CP Level for display    */
call do_the_test           /*  Execute the test                */
call send status           /*  Send the result in status to bb */
call close_term_socket     /*  Close and terminate the socket  */
exit
 
do_the_test:
/*  Check logged on flag, if true send yellow icon with message.  */
if notloggedon=1 then do
  msg = ptksvm 'Not logged on'
  msg = 'yellow' dow month day time() tz year msg
  status = 'status' machine||'.osa' msg
  status2 = 'status' machine||'.osa' msg
  return
end
color='green'
'pipe vmc 'ptksvm 'vswitch',
'| drop 6',
'| sort w2',
'| specs w2 1.10 w1 nw.10 w3 nw.10',
'| preface literal Vswitch    Port       Controller',
'| stem dio.'
out.0=0
oldvswitch='LEEG'
countports=0
zz = 0
out.1=dio.1
out.0=1
do d=2 to dio.0
 Parse Value dio.d With vswitch port controller
 if oldvswitch = 'LEEG' then oldvswitch=vswitch
 if oldvswitch \= vswitch then do
   if countports <=1 then do
     color='red'
     zz = out.0+1
     out.zz = oldvswitch' Missing Port'
     out.0=zz
     countports=0
    end
    oldvswitch=vswitch
 end
 countports= countports+1
 zz= out.0+1
 out.zz = vswitch'   'port'    'controller
 out.0 = zz
end
 
if countports <=1 then do
 color='red'
 zz = out.0+1
 out.zz = vswitch' Missing Port'
 out.0=zz
end
'pipe stem out. | specs w1 1.10 w2 nw.10 w3 nw.10 | join * x15 | var lstat'
/* check in HOBVM090 CLIENT if there are errors on the vswitch */
'pipe < hobvm090 client | all /QDIO/!/Reason:/ |nlocate /vdev/ ',
  '| join * | split before string /Adapter/ | locate /Reason/ ',
  '| specs /<b>/ 1 w1 nw /:/ n w11;* nw ,</b>, n ',
  '| var errmsg'
msg = color dow month day time() tz year cr
if errmsg\='ERRMSG' then do
 color='red'
 msg = msg||errmsg||cr
end
status = 'status' machine||'.osa' msg||cr||lstat
/* 'pipe var status | split at x15 | console' */
return
 
init_open_socket:
ret=Socket('Initialize','SOCOSA')
parse var ret rc .
if rc<>0 then
  say emsgstart 'Initialize rc=' ret
ret=Socket('Socket')
parse var ret rc .
if rc<>0 then
  say emsgstart 'Socket rc=' ret
parse var ret rc sid .
ret=Socket('Setsockopt',sid,'SOL_SOCKET','SO_ASCII','On')
parse var ret rc .
if rc<>0 then
  say emsgstart 'Setsockopt rc=' ret
name='AF_INET' bbport bbdisplay
ret=Socket('Connect',sid,name)
parse var ret rc .
if rc<>0 then
  say emsgstart 'Connect rc=' ret
ret=socket('Gethostname')
parse var ret rc hostname .
if rc<>0 then
  say 'Gethostname:' ret
ret=socket('Getdomainname')
parse var ret rc domainame .
if rc<>0 then
  say 'Getdomainname:' ret
/* fullname=hostname||'.'||domainame */
/*  Change periods to commas  */
/* fullname=translate(fullname,',','.') */
/* 'globalv put hostname' */
/* 'globalv put domainame' */
/* 'globalv put fullname' */
bbdate=date('U')
'globalv put bbdate'
return
 
close_term_socket:
ret=Socket('Close',sid)
parse var ret rc .
if rc<>0 then
  say emsgstart 'Close rc=' ret
ret=Socket('Terminate')
parse var ret rc .
if rc<>0 then
  say emsgstart 'Terminate rc=' ret
return
 
send:
parse arg out_data
ret=Socket('Send',sid,out_data)
parse var ret rc .
if rc<>0 then
  say emsgstart 'Send rc=' ret
return
 
get_cplevel:
'execio * CP (stem' CPLEVEL. 'string Q CPLEVEL'
return
 
get_timestamp:
'PIPE CP QUERY TIMEZONE' ,
  '| locate /Active/' ,
  '| specs w1 1' ,
  '| var TZ'
dow = substr(date('W'),1,3)
parse value date('N') with day month year .
timestamp = dow month day time() tz year
return
