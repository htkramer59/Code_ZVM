/*---------------------------------------------------------------+
| Function : Read the lun definitions                            |
|                                                                |
| File     : HOBLUNS  EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called   : HOBLUNS  .                                          |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20220120 16:34:32                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg arg1 arg2 . '(' dryrun .
Parse Source . . myfn myft .
If dryrun<>'' Then test='1'
              Else test='0'
/*---------------------------------------------------------------+
| Get the variables from our caller                              |
+---------------------------------------------------------------*/
'PIPE (NAME HOBLUNS.EXEC:26 END ?)',
   'rexxvars 1 toload',                          /* Take from caller */
   '| varload'                                   /* Hand 2 rexx      */
 
'PIPE (NAME HOBLUNS.EXEC:30 END ?)',
   'CP QUERY USERID',                     /* Ask CP           */
   '| specs w3.1',                        /* Keep this        */
   '| var node',                          /* Store as         */
   '| specs -2;-1',                       /* Reduce to this   */
   '| var shortnode'                      /* Hand 2 rexx      */
 
If \test Then Call Init_Open_Socket      /*  Initialize and Open the socket  */
Call Collect_Info          /*  Execute the test                */
If \test Then Do
              Call Send status           /*  Send the result in status to bb */
              Call Close_Term_Socket     /*  Close and terminate the socket  */
              End
         Else Do
              'PIPE (NAME HOBLUNS.EXEC:44 END ?)',
                'var status',                    /* Take this        */
                '| split at x15',                /* Split            */
                '| strip',                       /* Remove spaces    */
                '| cons'                         /* Show             */
              End
exit
 
 
Collect_Info:
/*---------------------------------------------------------------+
| VMUTIL collects LUN data several times a day                   |
+---------------------------------------------------------------*/
Address CMS 'REACC QUIET'                        /* Reaccess for the latest data */
If hostname='' |,
   hostname='HOSTNAME' Then hostname=node
'PIPE (NAME HOBLUNS.EXEC:60 END ?)',
   '< LUNLIST 'hostname' I',                     /* Read this file   */
   '| drop 1',                                   /* Ignore header    */
   '| f: fanout',                                /* Duplicate records*/
   '| join * x15',                               /* Make 1 record    */
   '| var status',                               /* Hand 2 rexx      */
   '? f:',                                       /* Conn 2 fanout    */
   '| locate anycase ~Potential~',               /* Select these     */
   '| count lines',                              /* Count them       */
   '| var missing'                               /* Hand 2 rexx      */
 
color='green'
If missing <> '0' Then color='yellow'
 
msg = color dow month day time() tz year cr
status = 'status' machine||'.luns 'color TimeStamp('TZ') status
return
 
 
DoSock:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Parse Arg sockinput
ret=Socket(sockinput)
parse var ret rc .
If rc<>0 Then Say TimeStamp('TZ')' : 'sockinput' rc=' ret
Return
 
 
Init_Open_Socket:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DoSock 'Initialize,SOCLUNS'
Call DoSock 'Socket'
Parse Var ret rc sid .
Call DoSock 'Setsockopt,'sid',SOL_SOCKET,SO_ASCII,On'
name='AF_INET' bbport bbdisplay
Call DoSock 'Connect,'sid','name
Call DoSock 'Gethostname'
Parse Var ret . hostname .
Call DoSock 'Getdomainname'
Parse Var ret . Domainame .
fullname=hostname||'.'||domainame
If fqdn='Y' Then machine=fullname
            Else machine=hostname
Return
 
 
Close_Term_Socket:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DoSock 'Close,'sid
Call DoSock 'Terminate'
Return
 
 
Send:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
parse arg out_data
Call DoSock 'SEND,'sid','out_data
Return
 
 
TimeStamp:
/*---------------------------------------------------------------+
| Return a timestamp and with TZ switch a linux like one         |
+---------------------------------------------------------------*/
Parse Upper Arg sw .
If sw='TZ' Then Do
   now=Date('S')
   Parse Var now yyyy +4 mm +2  dd +2 .
   'PIPE (NAME HOBLUNS.EXEC:136 END ? END.SKELETON:55)',
      'cp QUERY TIMEZONE',
      '| pick anycase w-1;-1 == ~ACTIVE~',
      '| specs w1.1',
      '| var tz'
   timest=Substr(Date('W'),1,3) mm dd time() tz yyyy
   End
   Else timest=Date('S') Time() myfn
Return timest
