/*---------------------------------------------------------------+
| Function : Add or change the INCLUde statement                 |
|                                                                |
| File     : DM_IPL   EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    :                                                     |
|            DM_GETID EXEC                                       |
|            DM_ONLIN EXEC                                       |
|                                                                |
| Called   : DM_IPL   <uid> <ipl statement>                      |
|          : IPL      <uid> <ipl statement>                      |
|                     QRY                                        |
|                     DEL                                        |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, home_office,                            |
| Created  : 20240630 13:14:19                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg uid newipl parameters
Parse Source . . myfn myft . .
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DMGETVAR
If show  Then Say Date('S') Time() myfn myft' Starting'
If test  Then Nop
If trace Then Do
              If tracetarget=myfn |,
                 tracetarget=''   Then Call Trace tracectl
              End
/*---------------------------------------------------------------+
| Get the uid and the directory                                  |
+---------------------------------------------------------------*/
Address CMS 'DM_GETID 'uid
'PIPE (NAME DM_IPL.EXEC:40 END ?)',
    'stem uid.',                                 /* Take these       */
    '| pick anycase w2.1 == ~IPL~',              /* Select this      */
    '| var curipl',                              /* Hand 2 rexx      */
    '| count lines',                             /* How many         */
    '| var ipl'                                  /* Store as boolean */
/*---------------------------------------------------------------+
| Take action on the input                                       |
+---------------------------------------------------------------*/
Select
When newipl='QRY' Then Call ShowIPL
When newipl='DEL' Then Call DelIPL
Otherwise Do
          If \ipl Then Call AddIPL
                  Else Call ReplIPL
          End
End
/*---------------------------------------------------------------+
| Put all online                                                 |
+---------------------------------------------------------------*/
Address CMS 'DM_ONLIN'
 
 
Exit:
/*---------------------------------------------------------------+
| Exit point                                                     |
+---------------------------------------------------------------*/
Parse Arg exrc .
If \Datatype(exrc,'NUM') Then exrc='0'
If show  Then Say Date('S') Time() myfn myft' Exiting'
Exit exrc
 
 
ShowIPL:
/*---------------------------------------------------------------+
| Show the IPL                                                   |
+---------------------------------------------------------------*/
curipl=SubWord(curipl,3)
Say Date('S') Time() myfn myft' IPL for 'uid' is: 'curipl
Call Exit
Return
 
 
ReplIPL:
/*---------------------------------------------------------------+
| Replace the existing IPL with a new one                        |
+---------------------------------------------------------------*/
'PIPE (NAME DM_IPL.EXEC:87 END ?)',
   'var curipl',                               /* Take this        */
   '| specs w1.2 1 ~'newipl parameters'~ nw',           /* Rework record    */
   '| var newipl'                              /* Hand 2 rexx      */
'PIPE (NAME DM_IPL.EXEC:91 END ?)',
   'stem direct.',                               /* Take these       */
   '| change ~'curipl'~ ~'newipl'~',             /* Replace...       */
   '| specs 7;* 1.80',                           /* Remove recnos    */
   '| > 'dirfnft ws' FIXED 80'                   /* Write 2 file     */
Return
 
 
AddIPL:
/*---------------------------------------------------------------+
| Add a IPL statement                                            |
+---------------------------------------------------------------*/
'PIPE (NAME DM_IPL.EXEC:103 END ?)',
   'stem uid.',                                  /* Take these       */
   '| pick anycase w2.1 == ~USER~ or',           /* Find this        */
                  'w2.1 == ~IDENTITY~ or',
                  'w2.1 == ~INCLUDE~ or',
                  'w2.1 == ~ACCOUNT~ or',
                  'w2.1 == ~MACHINE~ or',
                  'w2.1 == ~OPTION~ ',
   '| take last 1',
   '| var curstmt',                               /* Hand 2 rexx      */
   '| specs 1;* 1 ~;'Copies(' ',8)'IPL 'newipl parameters'~ nw',   /* Compose new one  */
   '| var newstmt'                                /* Hand 2 rexx      */
'PIPE (NAME DM_IPL.EXEC:115 END ?)',
   'stem direct.',                               /* Take these       */
   '| change ~'curstmt'~ ~'newstmt'~',           /* Replace          */
   '| split at ;',                               /* Split here       */
   '| specs 7;* 1.80',                           /* Remove recnos    */
   '| > 'dirfnft ws' FIXED 80'                   /* Write 2 file     */
Return
 
 
DelIPL:
/*---------------------------------------------------------------+
| Remove the DATEFORMAT statement                                |
+---------------------------------------------------------------*/
'PIPE (NAME DM_IPL.EXEC:128 END ?)',
   'stem direct.',                               /* Take these       */
   '| nlocate anycase ~'curipl'~',             /* Ignore this one  */
   '| specs 7;* 1.80',                           /* Remove recnos    */
   '| > 'dirfnft ws' FIXED 80'                   /* Write to file    */
return
