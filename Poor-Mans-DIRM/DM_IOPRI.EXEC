/*---------------------------------------------------------------+
| Function : Show, Add, Delete or Change IOPRIORITY stmt         |
|                                                                |
| File     : DM_IOPRI EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.2.0                                               |
|                                                                |
| Files    : <dirfnft>                                           |
|            DM_GETID EXEC                                       |
|            DM_ONLIN EXEC                                       |
|                                                                |
| Called   : DM_IOPRI <uid> <newprio> <low> <high>               |
|          : IOPRIOR  <uid> <newprio> <low> <high>               |
|                           QRY                                  |
|                           DEL                                  |
|                           RELATIVE                             |
|                           ABSOLUTE                             |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, home_office,                            |
| Created  : 20240709 12:02:13                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg uid newprio low high .
Parse Source . . myfn myft . .
/*---------------------------------------------------------------+
| Get some settings                                              |
+---------------------------------------------------------------*/
Call DMGETVAR
If show  Then Say Date('S') Time() myfn myft' Starting'
If test  Then Nop
If trace Then Do
              If tracetarget=myfn |,
                 tracetarget=''   Then Call Trace tracectl
              End
/*---------------------------------------------------------------+
| Get the userid and other directory stmts                       |
+---------------------------------------------------------------*/
Address CMS 'DM_GETID 'uid
/*---------------------------------------------------------------+
| Look for the correct stmts                                     |
+---------------------------------------------------------------*/
'PIPE (NAME DM_IOPRI.EXEC:45 END ?)',
   'stem uid.',                                  /* Take these       */
   '| pick anycase w2.1 == ~IOPRIORITY~',        /* Look 4 this      */
   '| var curprio',                              /* Hand 2 rexx      */
   '| count lines',                              /* How many         */
   '| var ioprio'                                /* Store as boolean */
/*---------------------------------------------------------------+
| Act on the input                                               |
+---------------------------------------------------------------*/
Select
When newprio='QRY'      Then Call ShowPrio
When newprio='DEL'      Then Call DelPrio
When newprio='RELATIVE' Then Call ChangePrio
When newprio='ABSOLUTE' Then Call ChangePrio
Otherwise Call Exit 810
End
/*---------------------------------------------------------------+
| Apply changes to the directory                                 |
+---------------------------------------------------------------*/
'PIPE (NAME DM_IOPRI.EXEC:64 END ?)',
   'stem direct.',
   chgstmt,
   '| specs 7;* 1.80',
   '| > 'dirfnft ws' FIXED 80'
If rc<>0 Then Call Exit 128
/*---------------------------------------------------------------+
| Check and put online the USER DIRECT                           |
+---------------------------------------------------------------*/
Address CMS 'DM_ONLIN'
If rc<>0 Then Call Exit rc
 
Exit:
/*---------------------------------------------------------------+
| Exit point                                                     |
+---------------------------------------------------------------*/
Parse Arg exrc .
If \Datatype(exrc,'NUM') Then exrc='0'
If show  Then Say Date('S') Time() myfn myft' Exiting'
Exit exrc
 
 
ShowPrio:
/*---------------------------------------------------------------+
| Show the current IOPRIORITY stmt                               |
+---------------------------------------------------------------*/
Say Date('S') Time() myfn myft' IOPRIORITY for 'uid' is set to:'
Say Copies(' ',25) Subword(curprio,3)
Call Exit
Return
 
 
DelPrio:
/*---------------------------------------------------------------+
| Delete the IOPRIORITY stmt                                     |
+---------------------------------------------------------------*/
chgstmt='| nlocate anycase ~'curprio'~'
Return
 
 
ChangePrio:
/*---------------------------------------------------------------+
| Change or ADD an IOPRIORITY stmt                               |
+---------------------------------------------------------------*/
If low<0  | low>255  Then Call Exit 811
If high<0 | high>255 Then Call Exit 812
If ioprio Then newprio=Subword(curprio,1,2) newprio low high
          Else Do
               'PIPE (NAME DM_IOPRI.EXEC:112 END ?)',
                  'stem uid.',                   /* Take these       */
                  '| pick anycase w2.1 == ~IPL~ or', /* Find any of      */
                                 'w2.1 == ~OPTION~', /* .                */
                  '| take last 1',               /* Keep 1           */
                  '| var curprio'                /* Hand 2 rexx      */
                newprio=curprio';'Copies(' ',8)'IOPRIORITY 'newprio low high
                End
chgstmt='| change anycase ~'curprio'~ ~'newprio'~ | split at ;'
Return
