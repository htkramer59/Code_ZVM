/*---------------------------------------------------------------+
| Function : Create a MDISKMAP                                   |
|                                                                |
| File     : MAP      EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : USER     DIRECT    input                            |
|            USER     MDISKMAP  output, depends on arguments     |
|            USER     FREEXT    output, depends on arguments     |
|            USER     USEDXT    output, depends on arguments     |
|            DM_VMLNK EXEC                                       |
|                                                                |
|                                                                |
| Called   : DM_MAP   <switch> ( <userfn> <userft>               |
|                     .         Creates USER MDISKMAP            |
|                     FREE      Creates USER FREEXT              |
|                     USED      Creates USER USEDXT              |
|                     ?         Shows helpd                      |
|           <userfn> <userft>   filename of DIRECTORY            |
|                                                                |
| Enduser calls: MAP, FREE or USED                               |
|                                                                |
| Comments : Usable with a monolithic user directory             |
|            VMLINK DIRECT requires an entry in VMLINK NAMES     |
|            Assumes 3390 devices                                |
|            Usable as PIPE stage when EXECLOADed with           |
|            filetype REXX                                       |
|                                                                |
| Author   : H.T.Kramer, home_office,                            |
| Created  : 20240618 18:25:19                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg what . '(' dirfnft
Parse Source . . myfn myft .
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Select
When myft='EXEC' Then Do
                      pipecmd='PIPE'
                      break='| specs a: 1.11 .',
                                       '12;* 12',
                                       'break a',
                                       'id a 1.11'
                      End
When myft='REXX' Then Do
                      pipecmd='CALLPIPE'
                      break=''
                      End
Otherwise Call Exit '999'
End
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DMGETVAR
If show  Then Say Date('S') Time() myfn myft' Starting 'what
If test  Then Nop
If trace Then Do
              If tracetarget=myfn |,
                 tracetarget=''   Then Call Trace tracectl
              End
/*---------------------------------------------------------------+
| Define the headers                                             |
+---------------------------------------------------------------*/
hdrs='; ;'TimeStamp()';Userid DevT Start End   Size  Owner   CUU   Flags       Subc;'||,
                      '------ ---- ----- ----- ----- ------- ----- ----------- ----'
/*---------------------------------------------------------------+
| Process  the arguments                                         |
+---------------------------------------------------------------*/
Select
When what=''     Then Do
                      used=0
                      gap=0
                      outft='MDISKMAP'
                      End
When what='FREE' Then Do
                      used=0
                      gap=1
                      outft='FREEXT'
                      End
When what='USED' Then Do
                      used=1
                      gap=0
                      outft='USEDXT'
                      End
When what='?'    Then Call Help
Otherwise Call Exit '228' what
End
Select
When pipecmd='PIPE'     Then pipout='| > USER 'outft ws
When pipecmd='CALLPIPE' Then pipout='| *:'
Otherwise Nop
End
/*---------------------------------------------------------------+
| Get the no of cylinders for each DASD volume                   |
+---------------------------------------------------------------*/
Call Get_Volume_Size
/*---------------------------------------------------------------+
| Access the location of the DIRECT file                         |
+---------------------------------------------------------------*/
wfm=DM_VMLNK('SOURCEDIR')
If wfm='' Then Call Exit '98'
/*---------------------------------------------------------------+
| Check if the directory file is there                           |
+---------------------------------------------------------------*/
pipecmd' (NAME DM_MAP.EXEC:108 END ?)',
    'literal 'dirfnft' *',                       /* Input            */
    '| state',                                   /* Does it exist    */
    '| count lines',                             /* How many?        */
    '| var dirok'                                /* Store as boolean */
If \dirok Then Call Exit 299
/*---------------------------------------------------------------+
| Read user direct and select the items...                       |
+---------------------------------------------------------------*/
pipecmd' (NAME DM_MAP.EXEC:117 END ?)',
    '< 'dirfnft wfm,                             /* Read monolith    */
    '| specs 1.72 1',                            /* Keep this        */
    '| strip',                                   /* Remove spaces    */
    '| pick 1.1 \== ~*~ and',                    /* Ignore comments  */
           '1.1 \== ~ ~ and',                    /* ..and emptoes    */
           '1.1 \== ~~',                         /* .                */
    '| p: pick anycase w1.1 == ~USER~ or',       /* Select these     */
                      'w1.1 == ~IDENTITY~ or',   /* .                */
                      'w1.1 == ~SUBCONFIG~ or',  /* .                */
                      'w1.1 == ~PROFILE~',       /* .                */
    '| specs w2.1 1 x40 n',                      /* Add space 2 end  */
    '| j: juxtapose',                            /* Join id & mdisk  */
    '| nlocate anycase ~MVSDUMMY~',              /* Remove these     */
    '| nlocate anycase ~$$$$$$~',                /* .                */
    '| specs w-2;-2 1.6',                        /* Adjust output    */
            'w-1;-1 nw',                         /* .                */
            'pad 0',                             /* .                */
            'w3.1 nw.5 right',                   /* .                */
            'w4.1 nw.5 right',                   /* .                */
            'pad space',                         /* .                */
            'w1.1 nw.8',                         /* .                */
            'w2.1 nw.4 right',                   /* .                */
    changeend,                                   /* Replace ends...  */
    '| sort w1.4',                               /* Sort on vol label*/
    '| specs w1.3 1',                            /* Calc endcyl      */
            'a: w3.1 .',                         /* .                */
            'b: w4.1 .',                         /* .                */
            'set #0:=a+b-1',                     /* .                */
            'pad 0',                             /* .                */
            'print #0 nw.5 right',               /* .                */
            'w4;* nw',                           /* .                */
    '| stem mdisk.',                             /* Hand 2 rexx      */
    '? p:',                                      /* Conn 2 pick      */
    '| pick w1.1 == ~MDISK~',                    /* Select these     */
    '| specs w2.1 1 w4.3 nw w3.1 nw',            /* Keep this info   */
    '| j:'                                       /* Conn 2 juxtapose */
If rc<>0 Then Call Exit rc 'PIPE'
/*---------------------------------------------------------------+
| Remove access to the DIRECT file                               |
+---------------------------------------------------------------*/
wfm=DM_VMLNK('SOURCEDIR DET')
/*---------------------------------------------------------------+
| Remove overlaps, find all the gaps even at the end of volume   |
+---------------------------------------------------------------*/
new.0=0
nstat.0=0
Do e=1 To endcyl.0
   label=SubWord(endcyl.e,1,1)
   size=Word(endcyl.e,5)
   pipecmd' (NAME DM_MAP.EXEC:167 END ?)',
      'stem mdisk.',                             /* Take these       */
      '| sort w1.4',                             /* In right order   */
      '| p: pick anycase w1.1 == ~'label'~ and', /* Select these     */
                'w3.1 == ~00000~ and',           /* .                */
                'w4.1 == ~'size-1'~',            /* .                */
      '| specs 1;* 1 ~**Overlap**~ nw',          /* Add marker       */
      '| stem overl.',                           /* Hand 2 rexx      */
      '? p:',                                    /* Conn 2 pick      */
      '| pick anycase w1.1 == ~'label'~',        /* Select lbl&size  */
      '| stem size.'                             /* Hand 2 rexx      */
/*---------------------------------------------------------------+
| Find the gaps, also at the end of the dasd                     |
+---------------------------------------------------------------*/
   Call Check_Gaps
   Call Check_Last_Cyl
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
   If gap Then keepgap='| locate ~**GAP**~'
          Else keepgap=''
   pipecmd' (NAME DM_MAP.EXEC:188 END ?)',
      'stem size.',                              /* Take these       */
      keepgap,                                   /* Keep gaps?       */
      '| if: if pick substr -2;-2 of w6.1 == ~-~',   /* Mark subconfigs  */
      '|   specs 1;* 1 ~+~ 57.1',                /* .                */
      '| if:',                                   /* Conn 2 if        */
      '| stem stats.',                           /* .                */
      break,                                     /* .                */
      '| preface var hdrs',                      /* Add headers      */
      '| split at ;',                            /* Split here       */
      '| stem new. append'                       /* Add 2 stem       */
   Call AddStats
End e
Call CalcStats
/*---------------------------------------------------------------+
| Write the diskmap file                                         |
+---------------------------------------------------------------*/
If used Then removegap='| nlocate ~**GAP**~'
        Else removegap=''
 
pipecmd' (NAME DM_MAP.EXEC:208 END ?)',
   'stem new.',                                  /* Take these       */
   removegap,                                    /* Keep gaps?       */
   '| preface stem stats.',
   pipout                                        /* Write or...      */
If rc<>0 Then Call Exit rc 'PIPE'
 
 
Exit:
/*---------------------------------------------------------------+
| General exit point                                             |
+---------------------------------------------------------------*/
Parse Arg exrc errmsg
If \Datatype(exrc,'NUM') Then exrc=0
If show  Then Say Date('S') Time() myfn myft' Exiting'
Exit exrc
 
 
CalcStats:
/*---------------------------------------------------------------+
| Calculate the statistics                                       |
+---------------------------------------------------------------*/
pipecmd' (NAME DM_MAP.EXEC:230 END ?)',
   'stem nstat.',                                /* Take these       */
   '| specs w1;* 1',                             /* Add percentages  */
           'a: w2.1 .',                          /* .                */
           'b: w4.1 .',                          /* .                */
           'c: w6.1 .',                          /* .                */
           'set (#0:=b/a*100;#1:=c/a*100)',      /* .                */
           'print #0 pict --9.99 nw',            /* .                */
           'print #1 pict --9.99 nw',            /* .                */
   '| if: if pick w4.1 == ~0~ and w6.1 == ~0~',  /* If these         */
   '|        spec w1.3 1 w2.1 nw w5.2 nw ~100.00  ~ nw w-1;-1 nw',   /* Replace 1 w max  */
   '| if:',                                      /* Conn 2 if        */
   '| fo: fanout',                               /* Dupl records     */
   '| buffer',                                   /* Prevent loop     */
   '| fi: fanin',                                /* Take in others   */
   '| specs w1.1 1.8',                           /* Adjust layout    */
           'w2.1 nw.10 right',                   /* .                */
           'w4.1 nw.10 right',                   /* .                */
           'w7.1 nw.6  right',                   /* .                */
           'w6.1 nw.10 right',                   /* .                */
           'w8.1 nw.6  right',                   /* .                */
   '| preface literal Label  Max_Cyls      Used_Cyls Used_% Free_Cyls  Free_%',   /* Add hdr          */
   '| stem stats.',                              /* Hand 2 rexx      */
   '? fo:',                                      /* Conn 2 fanout    */
   '| specs printonly eof',                      /* Calculate totals */
           'w1.1 1',                             /* .                */
           'a: w2.1 .',                          /* .                */
           'b: w4.1 .',                          /* .                */
           'c: w6.1 .',                          /* .                */
           'set (#0+=a;#1+=b;#2+=c)',            /* .                */
           'eof',                                /* .                */
           '~Totals:~ 1',                        /* .                */
           'print #0 pict ------9 nw',           /* .                */
           'print #1 nw',                        /* .                */
           'print #2 nw',                        /* .                */
   '| specs 1;* 1',                              /* Add percentages  */
           'a: w2.1 .',                          /* .                */
           'b: w3.1 .',                          /* .                */
           'c: w4.1 .',                          /* .                */
           'set (#0:=b/a*100;#1:=c/a*100)',      /* .                */
           'print #0 pict --9.99 nw',            /* .                */
           'print #1 pict --9.99 nw',            /* .                */
   '| specs w1.2 1 ~FF~ nw w3.1 nw ~FF~ nw w4;* nw',   /* To match adjustmt*/
   '| buffer',                                   /* Prevent loop     */
   '| fi:'                                       /* Conn 2 fanin     */
Return
 
 
Check_Last_Cyl:
/*---------------------------------------------------------------+
| Check if the last cylinders are empty                          |
+---------------------------------------------------------------*/
pipecmd' (NAME DM_MAP.EXEC:283 END ?)',
   'stem size.',                                 /* Take these       */
   '| sort w1.3',
   '| take last 1',                              /* We need this info*/
   '| var line'                                  /* Store as         */
Parse Value line With lbl dtype strt endc .
pipecmd' (NAME DM_MAP.EXEC:289 END ?)',
    'stem endcyl.',                              /* Get these        */
    '| pick w1.1 == ~'lbl'~',                    /* Select this one  */
    '| specs w-1;-1 1',                          /* Take this        */
    '| var size'                                 /* Store as         */
nstrt=endc+1
nocyl=size-endc
nendc=nstrt+nocyl-1
If endc<size-1 Then Do
       new=lbl dtype Right(nstrt,5,0) Right(nendc,5,0) Right(nocyl,5,0) Right('**GAP**',25)
       pipecmd' (NAME DM_MAP.EXEC:299 END ?)',
          'stem size.',                          /* Take these       */
          '| buffer',                            /* Prevent stall    */
          '| append var new',                    /* Add this         */
          '| stem size.'                         /* Rebuild stem     */
       End
Return
 
 
AddStats:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
pipecmd' (NAME DM_MAP.EXEC:312 END ?)',
   'stem stats.',
   '| nlocate anycase ~**overlap**~',
   '| n: nlocate anycase ~**GAP**~',
   '| specs printonly eof',
           'a: w5.1 .',
           'set #0+=a',
           'eof',
           '~Used:~ 1',
           'print #0 nw',
   '| fi: fanin',
   '| join * x40',
   '| specs ~'Word(label,1) size'~ 1 1;* nw',
   '| stem nstat. append',
   '? n:',
   '| specs printonly eof',
           'a: w5.1 .',
           'set #0+=a',
           'eof',
           '~Free:~ 1',
           'print #0 nw',
   '| fi:'
Drop stat.
Return
 
 
Check_Gaps:
/*---------------------------------------------------------------+
| Find the gaps....                                              |
+---------------------------------------------------------------*/
g=0
gap.0=0
'PIPE stem size.| cons'
Do s=1 To size.0
   label=Subword(size.s,1,2)
   cyls=Word(size.s,5)
   If s>1 Then Do
          curstr=Word(size.s,3)
          curend=Word(size.s,4)
          If prevend+1<>curstr Then Do
                g=g+1
                gap.g=label Right(prevend+1,5,0) Right(curstr-1,5,0) Right(curstr-prevend-1,5,0) Right('**GAP**',25)
                gap.0=g
                End
          End
   prevstr=Word(size.s,3)
   prevend=Word(size.s,4)
   If s=size.0 Then Drop prevstr prevend
End s
/*---------------------------------------------------------------+
| Add the gaps and overlays back into the stem                   |
+---------------------------------------------------------------*/
pipecmd' (NAME DM_MAP.EXEC:363 END ?)',
   'stem size.',                                 /* Take these       */
   '| buffer',                                   /* Prevent loop     */
   '| append stem gap.',                         /* Add these        */
   '| append stem overl.',                       /* ...and these     */
   '| sort w1.3',                                /* Sort on lbl&size */
   '| stem size.'                                /* Hand 2 rexx      */
Return
 
 
Get_Volume_Size:
/*---------------------------------------------------------------+
| Get the max no of cyls per volume                              |
+---------------------------------------------------------------*/
pipecmd' (NAME DM_MAP.EXEC:377 END ?)',
   'cp QUERY DASD ALL',                          /* Ask CP           */
   '| pick anycase w1.1 == ~DASD~',              /* Keep these       */
   '| specs ~QUERY DASD DETAILS~ 1 w2.1 nw',     /* Compose new cmds */
   '| cp',                                       /* Hand 2 cp        */
   '| pick anycase w2.1 == ~CUTYPE~',            /* Select these     */
   '| change ~,~~',                              /* Remove comma's   */
   '| specs w10.1 1',                            /* Adjust these     */
           'substr 1.4 of w7.1 nw',              /* Keep devicetype  */
           '~99999 99999~ nw',                   /* ..and add        */
           'a: w13.1 .',                         /* .....end cylinder*/
           'set #0:=a-1',                        /* .                */
           'pad 0',                              /* .                */
           'print #0 nw.5 right',                /* .                */
   '| sort w1.1',                                /* Sort on vol label*/
   '| stem endcyl.',                             /* Hand 2 rexx      */
   '| specs x4F 1',                              /* Compose          */
           '~CHANGE /~ nw',                      /* ...change cmds   */
           'w1.2 n',                             /* .                */
           '~00000 00END/~ nw',                  /* .                */
           'w1.2 n',                             /* .                */
           '~00000~ nw',                         /* .                */
           'w5.1 nw',                            /* .                */
           '~/~ n',                              /* .                */
   '| join * x40',                               /* Make 1 record    */
   '| var changeend'                             /* Store as variable*/
Return
 
 
TimeStamp:
Return Date('S') Time() myfn
 
 
Help:
/*---------------------------------------------------------------+
| Show help                                                      |
+---------------------------------------------------------------*/
'VMFCLEAR'
pipecmd' (NAME DM_MAP.EXEC:415 END ?)',
   '< 'myfn myft' *',                            /* Read myself      */
   '| tolabel Parse',                            /* Up to here       */
   '| cons'                                      /* ...and display   */
Call Exit
Return
