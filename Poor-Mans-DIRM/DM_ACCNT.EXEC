/*---------------------------------------------------------------+
| Function : Change the account code                             |
|                                                                |
| File     : DM_ACCNT EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.2.0                                               |
|                                                                |
| Files    : <dirfnft>                                           |
|            DM_GETID EXEC                                       |
|            DM_VMLNK EXEC                                       |
|            DM_ONLIN EXEC                                       |
|                                                                |
| Called   : DM_ACCNT <uid> <account>                            |
|          : ACC      <uid> <account>                            |
|                                                                |
| Comments : .                                                   |
|                                                                |
| Author   : H.T.Kramer, home_office,                            |
| Created  : 20240627 10:42:23                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg uid account .
Parse Source . . myfn myft .
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call DMGETVAR
If show  Then Say Date('S') Time() myfn myft' Starting'
If test  Then Nop
If trace Then Do
              If tracetarget=myfn |,
                 tracetarget=''   Then Call Trace tracectl
              End
/*---------------------------------------------------------------+
| Simple check                                                   |
+---------------------------------------------------------------*/
If account='' Then Call Exit '101'
/*---------------------------------------------------------------+
| Get user details                                               |
+---------------------------------------------------------------*/
Address CMS 'DM_GETID 'uid
/*---------------------------------------------------------------+
| Find account stmt                                              |
+---------------------------------------------------------------*/
'PIPE (NAME DM_ACCNT.EXEC:46 END ?)',
   'stem uid.',                                  /* Take these   e   */
   '| pick anycase substr 1.3 of w2.1 == ~ACC~', /* Find acct stmt   */
   '| f: fanout',                                /* Dupl. record     */
   '| var orig',                                 /* Hand 2 rexx      */
   '? f:',                                       /* Conn 2 fanout    */
   '| specs w1.2 1 ~'account'~ nw',              /* Compose new one  */
   '| var new'                                   /* Hand 2 rexx      */
If rc<>0 Then Call Exit '102'
/*---------------------------------------------------------------+
| Apply the change                                               |
+---------------------------------------------------------------*/
'PIPE (NAME DM_ACCNT.EXEC:58 END ?)',
   'stem direct.',                               /* Take this        */
   '| change ~'orig'~ ~'new'~',                  /* Replace acct     */
   '| specs 7;* 1.80',                           /* Adjust recs      */
   '| > 'dirfnft ws' FIXED 80'                   /* Write 2 file     */
If rc<>0 Then Call Exit '128'                    /* .                */
/*---------------------------------------------------------------+
| All ok put it online                                           |
+---------------------------------------------------------------*/
Address CMS 'DM_ONLIN'
If rc<>0 Then Call Exit rc
 
Exit:
/*---------------------------------------------------------------+
| Exit point                                                     |
+---------------------------------------------------------------*/
Parse Arg exrc .
If \Datatype(exrc,'NUM') Then exrc=0
If show  Then Say Date('S') Time() myfn myft' Exiting'
Exit exrc
