/*==============================================================*/
/*    Program Name - XEDIT Column Editing/VM                    */
/*    Licensed Material - Program Property of IBM               */
/*                                                              */
/*    Copyright              (c) IBM Corp 1991                  */
/*--------------------------------------------------------------*/
/* Use this macro to invoke COL by entering H or a pair of HH   */
/* for a block to be altered.  This prefix routine is simular   */
/* to PREFIXH except that the line with the H or the first HH   */
/* is not moved to the current line, but rather, the current    */
/* line is moved to the H or first HH.  This tends to keep the  */
/* screen stable for small depths of COL.  On exit the screen   */
/* is restored to the origional position.                       */
/*                                                              */
/* Place the following in XEDIT PROFILE.                        */
/*                 SET PRE SYN H  PREFIXH2                      */
/*                 SET PRE SYN HH PREFIXH2                      */
/*--------------------------------------------------------------*/
trace off
numeric digits 10              /* Allow bigger numbers */
arg pref func pline op extra
parse source . . . . . name .
prf = name || space(op extra)
If pref ^= 'PREFIX' then call error1,
  "8 646E 'PREFIXH' must be invoked from the prefix area."
If func = 'CLEAR' then exit
'COMMAND EXTRACT /SCOPE/DISPLAY/TERMINAL/FNAME/FTYPE/FMODE/MSGM/LINE'
If func = 'SHADOW' & scope.1 = 'DISPLAY' Then call error,
  "0 661E Prefix '"name"' is invalid for the line on",
  "which it was entered."
If func ^= 'SET' & func ^= 'SHADOW' then call error1,
  "8 646E 'PREFIXH' must be invoked from the prefix area."
Select
    When length(name) = 1 then do   /* No block command */
      If extra ^= '' then call error,
        "0 659E Invalid prefix subcommand:" prf
      If op = '' then op = 1
      If (datatype(op,'W') & op > 0 & pos(op,'.') = 0) | op = '*' then do
        call varcurl pline
PUSH    'COMMAND :'pline 'MACRO COL' op
        End
      Else call error,
        "0 659E Invalid prefix subcommand:" prf
      End
    When length(name) = 2 then do   /* Block cmd */
      If op ^= '' then call error,
        "0 659E Invalid prefix subcommand:" prf
      'COMMAND EXTRACT /PENDING BLOCK' name ':0 :'pline '/'
      If pending.0 ^= 0 then do
        call varcurl pending.1
        'COMMAND :'pending.1 'COMMAND SET PENDING OFF'
        'COMMAND LOCATE :'pline
        'COMMAND SET SCOPE ALL'
        'COMMAND LOCATE :'pending.1
PUSH    'COMMAND :'pending.1 'MACRO COL :'pline + 1
PUSH    'COMMAND SET SCOPE DISPLAY'
        End
      Else do
       'COMMAND :'pline 'COMMAND SET PENDING BLOCK' left(prf,5)
       'COMMAND :'line.1
      End
      End
    Otherwise call error "0 686E Synonym '"name"' not",
      "recognized by prefix macro 'PREFIXH'."
    End
Exit
VARCURL: /* try to not to move first line on screen, by varying
             the CURLINE of XEDIT to the first COL affected line */
    address '' 'GLOBALV SELECT col GET CURLINE.2'
    act=curline.2^=''                         /* COL already active */
    'COMMAND SET MSGM OFF'
    'COMMAND EXTRACT /LINE' /*XEDIT BUG: get LINE.1 as CURSOR FILE ... */
    'COMMAND CURSOR FILE' arg(1) 'P 255' /* changes XEDIT current line*/
    'COMMAND :'line.1 'COMMAND EXTRACT /CURSOR/CURLINE/LINE/LSCREEN'
    if cursor.1>1 then do
        if cursor.1<lscreen.1-4 then 'COMMAND SET CURL ON 'cursor.1
                                else 'COMMAND SET CURL ON '7
        if ^act then address '' 'GLOBALV SELECT col PUT CURLINE.2 LINE.1'
    end
    'COMMAND SET MSGM' msgmode.1
return
/*  Error routines  */
error:  'COMMAND SET SCOPE ALL'
        'COMMAND :'pline 'COMMAND SET PENDING ERROR' left(prf,5)
        'COMMAND SET SCOPE' scope.1
error1: parse arg err msg
        'COMMAND RESTORE'
        If msg ^= '' then 'COMMAND EMSG PXU' || strip(msg,'L')
        Exit err
