/*---------------------------------------------------------------+
| Function : Look at page usage in memory & on dasd              |
|                                                                |
| File     : PAGEUSAG EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : PAGEUSAG FILE     output                            |
|                                                                |
| Called   : PAGEUSAG <source> <srtcl> ( <how>                   |
|                                                                |
|            <source>   can be ?, DIRM or ACTIVE                 |
|                                                                |
|                       ?      : shows help                      |
|                       DIRM   : takes userids from DIRMAINT     |
|                                directory source files (default)|
|                       ACTIVE : Takes userids from QUERY NAMES  |
|                                                                |
|                                                                |
|            <srtcl>    can be STOR, DASD, RES or WSS            |
|                                                                |
|                       STOR : Sorts output on current memory    |
|                              size (default)                    |
|                       DASD : Sorts output on pages on disk     |
|                       RES  : Sorts output on pages in memory   |
|                       WSS  : Sorts on WorkingSetSize pages     |
|                                                                |
|                                                                |
|            <how>      can be empty, SEL: or TOPnn              |
|                                                                |
|                       TOPnn : Show the top nn of the list      |
|                       SEL:  : selects a set of wildcards from  |
|                               the input.                       |
|                       empty : Complete list will be shown      |
|                                                                |
|                                                                |
| Comments : Need access to DIRMAINT source files!!              |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20200922 13:59:24                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg source srtcl'(' how
Parse Source . . myfn myft .
/*---------------------------------------------------------------+
| Determine the source for the user names, headers and actions   |
+---------------------------------------------------------------*/
If source='' Then source='DIRM'
Select
When source='DIRM'   Then Do
     drm=1
     header='          On_DASD          Resident           Working_set_size Memory(directory) Memory(actual);'||,
            'User      Pages   MB       Pages   MB         Pages   MB       Value    MB       Value    MB;'||,
            '====      =====   ==       =====   ==         =====   ==       =====    ==       =====    =='
     Call Get_Dirm_Info
     End
When source='ACTIVE' Then Do
     drm=0
     header='         On_DASD            Resident          Working_set_size  Storage;'||,
            'User     Pages    MB        Pages   MB        Pages    MB       Asis   Mb;'||,
            '====     =====    ==        =====   ==        =====    ==       ====   =='
     Call Get_Query_Names
     End
When source='?'      Then Do
     'VMFCLEAR'
     'PIPE (NAME PAGEUSAG.EXEC:67 END ?)',
         '< 'myfn myft' *',
         '| tolabel Parse',
         '| cons'
     Exit
     End
Otherwise Call Exit 27 'Invalid source given: 'source
End
/*---------------------------------------------------------------+
| Determine the columns to sort on                               |
+---------------------------------------------------------------*/
If srtcl='' Then srtcl='STOR'
Select
When srtcl='STOR' Then sortcol='3'
When srtcl='DASD' Then sortcol='5'
When srtcl='RES'  Then sortcol='7'
When srtcl='WSS'  Then sortcol='9'
Otherwise Call Exit 26 'Invalid sortcol given: 'srtcl
End
If drm Then sortcol=sortcol+2
/*---------------------------------------------------------------+
| Determine the selection given                                  |
+---------------------------------------------------------------*/
If Substr(how,1,3)='TOP'  Then Do
                          Parse Value how With 'TOP' number .
                          sel='| take first 'number
                          End
                          Else sel=''
If Substr(how,1,4)='SEL:' Then Do
                          Parse Value how With 'SEL:' selection
                          'PIPE (NAME PAGEUSAG.EXEC:97 END ?)',
                              'var selection',  /* Take this        */
                              '| split',        /* Split            */
                              '| specs x4F 1',  /* Compose wildcards*/
                                      '~WILDCARD w1.1 /~ nw',  /* .                */
                                      'w1.1 n ' /* .                */
                                      '~/~ n',  /* .                */
                              '| stem wildc.'   /* Store as         */
                          selection='Selection: 'selection
                          End
                          Else Do
                          wildc.0=0
                          selection=''
                          End
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Call Get_Indicate_Values
If drm Then Call CompleteOutput
If wildc.0<>0 Then Call Filter
/*---------------------------------------------------------------+
| Create output file                                             |
+---------------------------------------------------------------*/
'PIPE (NAME PAGEUSAG.EXEC:120 END ?)',
    'stem ind.',                                /* Take these       */
    '| sort w'sortcol'.1 D',                    /* Sort them        */
    '| if: if pick w-3;-3 \==  ~.~ and',
                  'w-3;-3 \== w-1;-1',
    '|    specs 1;* 1 ~<<<<~ nw',
    '| if:',
    sel,                                        /* Add selection    */
    '| preface  var totals',                    /* Add totlas       */
    '| preface var header',                     /* Add header       */
    '| preface literal ',
    '| preface var storsize',
    '| preface literal ',
    '| preface literal 'TimeStamp() selection 'Sorted on: 'srtcl ,  /* ..more details   */
    '| split at ;',                             /* Split here       */
    '| > 'myfn' FILE A'                         /* Write 2 file     */
Address CMS 'XEDIT 'myfn' FILE A'
 
 
Exit:
/*---------------------------------------------------------------+
| Universal way to exit                                          |
+---------------------------------------------------------------*/
Parse Arg exrc emsg
If \Datatype(exrc,'NUM') Then exrc=0
If exrc<>0 Then Do
      Say TimeStamp() emsg
      End
Exit exrc
 
 
TimeStamp:  /* Create a timestamp */
Return Date('S') Time() myfn
 
 
Filter:
/*---------------------------------------------------------------+
| Filter out the wildcards                                       |
+---------------------------------------------------------------*/
work.0=0                                        /* Initialise stem  */
Do w=1 To wildc.0
  'PIPE (NAME PAGEUSAG.EXEC:154 END ?)',
      'stem ind.',                              /* Take these       */
       wildc.w,                                 /* Run wildcard     */
       '| stem work. append'                    /* Add 2 stem       */
End w
'PIPE (NAME PAGEUSAG.EXEC:159 END ?)',
    'stem work.',                               /* Take these       */
    '| stem ind.'                               /* Store as these   */
Return
 
 
CompleteOutput:
/*---------------------------------------------------------------+
| Add the dirmaint info to the rest                              |
+---------------------------------------------------------------*/
'PIPE (NAME PAGEUSAG.EXEC:169 END ?)',
   'stem ind.',                                 /* Take these       */
   '| buffer',                                  /* Prevent loop     */
   '| append stem dirinfo.',                    /* Add this         */
   '| sort w1.1',                               /* Sort on id       */
   '| join keylen 8',                           /* Join on id       */
   '| if: if pick w5.1 == ~~',                  /* If this is empty */
   '|    specs w1.3 1',                         /* ...add this      */
              '~ . . . . . . . . ~ nw',         /* .                */
   '| if:',                                     /* Conn 2 if        */
   '| specs w1.1 1.8',                          /* Smarten          */
          ' w4.1 nw.8 right',                   /* ....the          */
          ' w5.1 nw.8 right',                   /* ......lay        */
          ' w6.1 nw.8 right',                   /* ........out      */
          ' w7.1 nw.8 right',                   /* ....of records   */
          ' w8.1 nw.8 right',                   /* .                */
          ' w9.1 nw.8 right',                   /* .                */
          ' w10.1 nw.8 right',                  /* .                */
          ' w11.1 nw.8 right',                  /* .                */
          ' w2.1 nw.8 right',                   /* .                */
          ' w3.1 nw.8 right',                   /* .                */
   '| stem ind.'                                /* Store as         */
Return
 
 
Get_Indicate_Values:
/*---------------------------------------------------------------+
| Issue indicate for the users found                             |
+---------------------------------------------------------------*/
'PIPE (NAME PAGEUSAG.EXEC:198 END ?)',
   'stem user.',                                /* Take these       */
   '| f: fanout',                               /* Duplicate recs   */
   '| specs w1.1 1.9',                          /* Keep this        */
   '| j: juxtapose',                            /* Join some stuf   */
   '| sort unique',                             /* Remove duplicates*/
   '| pick substr 1.3 of w2.1 == ~WS=~ or',     /* Select these     */
          'substr 1.5 of w2.1 == ~STOR=~ or',   /* .                */
          'substr 1.7 of w2.1 == ~INSTAN=~ or', /* .                */
          'substr 1.4 of w2.1 == ~RES=~',       /* .                */
   '| specs fs = w1.1 1.8',                     /* Remove text & =  */
                'f2 nw',                        /* .                */
   '| join keylen 8',                           /* Join on id       */
   '| specs w1.1 1.8',                          /* Rework layout    */
           'w4.1 nw.8 right',                   /* .                */
           'w4.1 nw',                           /* .                */
           'w2.1 nw',                           /* .                */
           'w3.1 nw',                           /* .                */
           'w5.1 nw',                           /* .                */
   '| siconv w3.1 g2m 16',                      /* Conv. gb to mb   */
   '| change anycase w3.1 ~M~~',                /* Remove mb sign   */
   '| specs w1.2 1',                            /* Smarten layout   */
           'pad 0',                             /* .                */
           'w3.1 nw.8 right',                   /* .                */
           'pad space',                         /* .                */
           'w4;* nw',                           /* .                */
   '| specs w1;3 1',                            /* Smarten layout   */
           'w4.1 nw.8 right',                   /* ..and            */
           'w5;* nw',                           /* ...Convert pages */
           '~;~ nw',                            /* ....to mb        */
           'pad 0',                             /* .                */
           'a: w4.1 .',                         /* .                */
           'set #0:=(a*4)/1024',                /* .                */
           'print #0 nw.8 right',               /* .                */
   '| specs w1;4 1',                            /* See previous     */
           'w5.1 nw.8 right',                   /* .                */
           'w6;* nw',                           /* .                */
           'pad 0',                             /* .                */
           'b: w5.1 .',                         /* .                */
           'set #1:=(b*4)/1024',                /* .                */
           'print #1 nw.8 right',               /* .                */
   '| specs w1;5 1',                            /* See previous     */
           'w6.1 nw.8 right',                   /* .                */
           'w7;* nw',                           /* .                */
           'pad 0',                             /* .                */
           'c: w6.1 .',                         /* .                */
           'set #2:=(c*4)/1024',                /* .                */
           'print #2 nw.8 right',               /* .                */
   '| specs w1.4 1',                            /* Smarten layout   */
           'w8.1 nw',                           /* .                */
           'w5.1 nw.8 right',                   /* .                */
           'w9.1 nw',                           /* .                */
           'w6.1 nw.8 right',                   /* .                */
           'w10.1 nw',                          /* .                */
   '| stem ind.',                               /* Store as         */
   '| specs printonly eof',                     /* Calculate        */
           'a: w3.1 .',                         /* ...some          */
           'b: w5.1 .',                         /* ....totals       */
           'c: w7.1 .',                         /* .                */
           'd: w9.1 .',                         /* .                */
           'set (#0+=a;#1+=b;#2+=c;#3+=d)',     /* .                */
           'eof',                               /* .                */
           '~Totals:~ 1.8',                     /* .                */
           'x40 nw.8',                          /* .                */
           'pad 0',                             /* .                */
           'print #0 nw.8 right',               /* .                */
           'pad space',                         /* .                */
           'x40 nw.8',                          /* .                */
           'pad 0',                             /* .                */
           'print #1 nw.8 right',               /* .                */
           'pad space',                         /* .                */
           'x40 nw.8',                          /* .                */
           'pad 0',                             /* .                */
           'print #2 nw.8 right',               /* .                */
           'pad space',                         /* .                */
           'x40 nw.8',                          /* .                */
           'pad 0',                             /* .                */
           'print #3 nw.8 right',               /* .                */
   '| siconv w2.1 m2g 8.2 G',
   '| siconv w3.1 m2g 8.2 G',
   '| siconv w4.1 m2g 8.2 G',
   '| siconv w5.1 m2g 8.2 G',
   '| specs w1.1 1',
           'w2.1 19.8 right',
           'w3.1 37.8 right',
           'w4.1 55.8 right',
           'w5.1 73.8 right',
   '| var totals',                              /* Store as         */
   '? f:',                                      /* Conn 2 fanout    */
   '| specs ~INDICATE USER~ 1 w1.1 nw',         /* Compose cmds     */
   '| cp',                                      /* Issue cmds       */
   '| strip',                                   /* Remove spaces    */
   '| split',                                   /* Split here       */
   '| j:',                                      /* Conn 2 juxtapose */
   '?',
   'cp QUERY STORAGE',
   '| var storsize'
Return
 
 
 
Get_Dirm_Info:
/*---------------------------------------------------------------+
| Get the list of users from DIRMAINT                            |
+---------------------------------------------------------------*/
Address CMS 'VMLINK DIRMAINT 1DF (NONAMES QUIET .fm'
If rc=0 Then Parse Pull . . wfm .
        Else Call Exit rc 'Problem accessing DIRMAINT 1DF: 'rc
'PIPE (NAME PAGEUSAG.EXEC:294 END ?)',
    'cms LISTFILE * CLUSTER 'wfm' (NOHEADER',   /* Get a list       */
    '| getfiles',                               /* Read the files   */
    '| specs 1.72 1',                           /* Remove lineno    */
    '| strip',                                  /* Remove spaces    */
    '| pick w1.1 == ~USER~ or',                 /* Select these     */
           'w1.1 == ~IDENTITY~',                /* .                */
    '| specs w2.1 1.8 w4.1 nw',                 /* Keep these vals  */
    '| pick w2.1 \== ~~',                       /* Remove wo memory */
    '| xlate upper',                            /* Upper case       */
    '| specs w1.1 1.8',                         /* Adjust           */
            'w2.1 nw.8 right ',                 /* .                */
            'w2.1 nw',                          /* .                */
    '| siconv w3.1 g2m 8',                      /* Convert GB to MB */
    '| change anycase w3.1 ~M~~',               /* Remove these     */
    '| specs w1.1 1.8',                         /* Rework layout    */
            'w2.1 nw.8 right',                  /* .                */
            'pad 0',                            /* .                */
            'w3.1 nw.8 right',                  /* .                */
    '| stem dirinfo.',                          /* Store as         */
    '| specs w1.1',                             /* Keep this        */
    '| stem user.'                              /* Hand 2 rexx      */
Address CMS 'VMLINK DIRMAINT 1DF <DET (NONAMES QUIET'
Return
 
 
Get_Query_Names:
/*---------------------------------------------------------------+
| Get the list of users from QUERY NAMES                         |
+---------------------------------------------------------------*/
'PIPE (NAME PAGEUSAG.EXEC:324 END ?)',
     'cp QUERY NAMES',                          /* Ask cp           */
     '| split at ,',                            /* Split here       */
     '| strip',                                 /* Remove spaces    */
     '| pick w1.1 \== ~VSM~',                   /* Ignore this one  */
     '| specs fs - f1',                         /* Keep this        */
     '| stem user.'                             /* Store as         */
Return
