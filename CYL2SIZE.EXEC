/*---------------------------------------------------------------+
| Function : Calculate the number of CYLINDERS needed            |
|                                                                |
| File     : SIZE2CYL EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called     : CYL2SIZE <size> <si-unit>                         |
| As function: out=size2cyl(<size> <si-unit>)                    |
|                                                                |
| Comments : <size>      the size you need (in bytes)            |
|            <si-unit>   the si unit you want                    |
|                                                                |
|            si-unit:    K = Kilo  1024 to the power of 1        |
|                        M = Mega                       2        |
|                        G = Giga                       3        |
|                        T = Tera                       4        |
|                        P = Peta                       5        |
|                        E = Exa                        6        |
|                        Z = Zetta                      7        |
|                        Y = Yotta                      8        |
|                                                                |
|            When called as function the return code is the      |
|            number of Cylinders                                 |
|                                                                |
| Example  : cyl2size 3000                                       |
| Output   :                                                     |
| 3000 cyls=2109.375M                                            |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20181122 17:02:51                                   |
| Changes  : .                                                   |
|            20250701 H.T.Kramer : Small correction to text      |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg size si_unit .
Parse Source . how myfn myft .
numeric digits 40 /* To display the full number */
blkpcyl=180       /* Blocks per cylinder */
blksize=4096      /* Bytes per block */
unit=1024         /* unit size of 1 K byte */
If size=''  Then Call Exit 28
If size='?' Then Do
                 'VMFCLEAR'
                 'PIPE (NAME CYL2SIZE.EXEC:47 END ?)',
                      '< 'myfn myft' *',
                      '| tolabel Parse',
                      '| cons'
                  Call Exit 4
                  End
bytes=size*blksize*blkpcyl
/*---------------------------------------------------------------+
| Default si_unit                                                 |
+---------------------------------------------------------------*/
If si_unit='' Then si_unit='M'
/*---------------------------------------------------------------+
| Determine the power factor                                     |
+---------------------------------------------------------------*/
Select
  When si_unit='K' Then Do ; power=1 ; si_pref='Kilo' ; End
  When si_unit='M' Then Do ; power=2 ; si_pref='Mega' ; End
  When si_unit='G' Then Do ; power=3 ; si_pref='Giga' ; End
  When si_unit='T' Then Do ; power=4 ; si_pref='Tera' ; End
  When si_unit='P' Then Do ; power=5 ; si_pref='Peta' ; End
  When si_unit='E' Then Do ; power=6 ; si_pref='Exa'  ; End
  When si_unit='Z' Then Do ; power=7 ; si_pref='Zetta'; End
  When si_unit='Y' Then Do ; power=8 ; si_pref='Yotta'; End
  Otherwise Do
            Say 'SI unit 'si_unit' is out of our league'
            Call Exit 99
            End
End
/*---------------------------------------------------------------+
| Actual calculations                                            |
+---------------------------------------------------------------*/
Do p=1 To power
   bytes=bytes/unit
End p
fbablocks=bytes||si_unit
/*---------------------------------------------------------------+
| Decide on how to present the outcome...                        |
+---------------------------------------------------------------*/
If how='FUNCTION' Then Call Exit fbablocks
                  Else Do
                       Say size' = 'fbablocks
                       Call Exit
                       End
Exit:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Parse Arg exrc .
If exrc='' | exrc='EXRC' Then exrc=0
Exit exrc
