/*---------------------------------------------------------------+
| Function : FORCE and XAUTOLOG a user id                        |
|                                                                |
| File     : RECYCLE  EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : TRACKVxx MODULE                                     |
|                                                                |
| Called   : RECYCLE  <user id> '(' <show>                       |
|                                                                |
|            <show>   Can only be CONS and will start TRACK(Vxx) |
|                     with the CONS parameter                    |
|                                                                |
| Comments : Requires CP CLASS A for FORCE & XAUTOLOG            |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20180529 06:57:46                                   |
| Changes  : .                                                   |
|            20220308 H.T.Kramer : Select the TRACKVxx MODULE    |
|                          for the CP LEVEL                      |
|            20201214 H.T.Kramer : Put a default for show in     |
|                          LASTING GLOBALV:                      |
|                          globalv select recycle setlp show cons|
|            20200702 H.T.Kramer : Do a XAUTOLOG,,               |
|                                                                |
|                                                                |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg userid wait  '(' show .
Parse Source . . myfn myft .
/*---------------------------------------------------------------+
| Check the input                                                |
+---------------------------------------------------------------*/
If userid='' Then Call Exit 28 'No input'
If userid='?' Then Do
          'VMFCLEAR'
          'PIPE (NAME RECYCLE.EXEC:38 END ?)',
              '< 'myfn myft' *',
              '| tolabel Parse',
              '| cons'
          Call Exit
          End
If wait='' Then wait='IMMED'
If show='' Then show=Value('SHOW',,'LASTING 'myfn)
/*---------------------------------------------------------------+
| Starting the action                                            |
+---------------------------------------------------------------*/
Address COMMAND 'CP FORCE 'userid wait
If rc<>0 & rc<>45 Then Call Exit rc 'Nonzero rc from FORCE'
Address COMMAND 'CP SLEEP 1 SEC'
Address COMMAND 'CP XAUTOLOG 'userid
/*---------------------------------------------------------------+
| Do we track the user id or not                                 |
+---------------------------------------------------------------*/
If show='CONS' Then Do
/*---------------------------------------------------------------+
| Check if we have access to the tracking stuff...               |
+---------------------------------------------------------------*/
          'PIPE (NAME RECYCLE.EXEC:60 END ?)',
              'cp QUERY CPLEVEL',
              '| take first 1',
              '| specs ~TRACKV~ 1 w3.1 n substr 1.1 of w5.1 n',
              '| var trackmodule'
          'PIPE (NAME RECYCLE.EXEC:65 END ?)',
              'literal 'trackmodule' MODULE',
              '| split ;',
              '| state',
              '| count lines',
              '| var trackmod'
          If trackmod Then Do
                    Address COMMAND 'CP SLEEP 1 SEC'
                    Address CMS trackmodule userid' CONS'
                   End
                   Else Call Exit rc 'TRACK MODULE not found'
          End
 
 
Exit:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Parse Arg exrc emsg
If \Datatype(exrc,'NUM') Then exrc=0
If exrc<>'0' Then Say emsg
Exit exrc
