/*---------------------------------------------------------------+
| Function : Scan the MDISK/Filepools of a user for a file       |
|                                                                |
| File     : SCANUSR  EXEC                                       |
+----------------------------------------------------------------+
| Version  : 1.1.0                                               |
|                                                                |
| Files    : .                                                   |
|                                                                |
| Called   : SCANUSR  <id> <search>                              |
|                                                                |
| Comments : <search> can be any filename/filetype combo         |
|                                                                |
| Author   : H.T.Kramer, ,                                       |
| Created  : 20220614 12:11:31                                   |
| Changes  : .                                                   |
|                                                                |
+---------------------------------------------------------------*/
Parse Upper Arg id search
Parse Source . . myfn myft myfm .
If id='?' Then Do
            'VMFCLEAR'
            'PIPE (NAME SCANUSR.EXEC:23 END ?)',
               '< 'myfn myft' *',                /* Read myself      */
               '| tolabel Parse',                /* Up to here       */
               '| cons'                          /* Show             */
             Call Exit
             End
If id=''     Then Call Exit '128 No id entered'
If search='' Then Call Exit '228 No search argument'
'PIPE (NAME SCANUSR.EXEC:31 END ?)',
   'cp QUERY MDISK USER 'id' 0 DIR LOC',         /* Ask cp           */
   '| find HCPQMD040E',                          /* If this          */
   '| count lines',                              /* Count lines      */
   '| var idok'                                  /* Store as         */
If \idok Then Call Exit '328 Userid 'id' does not exist'
dir.0=0;mdisk.0=0;result.0=0
fps='VMSYS VMSYSU VMPSFS VMUSERS'
'PIPE (NAME SCANUSR.EXEC:39 END ?)',
   'var fps',                                    /* Take these       */
   '| split',                                    /* Split...         */
   '| f: fanout',                                /* Duplicate recs   */
   '| specs w1.1 1.9',                           /* Keep this        */
   '| j: juxtapose',                             /* Join w other     */
   '| pick w3.1 == ~YES~',                       /* Select this      */
   '| specs ~LISTDIR~ 1',                        /* Compose cmds     */
           'w1.1 nw',                            /* .                */
           '~:~ n',                              /* .                */
           'w2.1 n',                             /* .                */
           '~. (ALL SUB~ n',                     /* .                */
   '| cms',                                      /* Issue cmds       */
   '| pick w1.1 == ~-~',                         /* Select these     */
   '| specs ~.DIR~ 1 w2.1 nw',                   /* Prep 4 later     */
   '| stem dir.',                                /* Hand 2 rex       */
   '? f:',                                       /* Conn 2 fanout    */
   '| specs ~QUERY ENROLL USER FOR 'id'~ 1',     /* Compose cmds     */
           'w1.1 nw',                            /* .                */
   '| cms',                                      /* Run them         */
   '| j:'                                        /* Conn 2 juxtapose */
'PIPE (NAME SCANUSR.EXEC:60 END ?)',
   'cp QUERY MDISK USER 'id' 0-FFFF DIR LOC',    /* Ask cp           */
   '| drop first 1',                             /* Ignore hdr       */
   '| pick w3.1 \== ~MAINT~ and',                /* Select not these */
          'w4.1 \== ~0190~ and',                 /* .                */
          'w4.1 \== ~019E~ and',                 /* .                */
          'w4 \== ~019D~',                       /* .                */
   '| specs w3.2 1',                             /* Keep this        */
   '| sort',                                     /* Sort them        */
   '| append stem dir.',                         /* Add these        */
   '| stem mdisk.',                              /* Hand 2 rexx      */
   '| specs ~VMLINK~ 1',                         /* Compose cmd      */
           'w1.2 nw',                            /* .                */
           '~(NONAMES QUIET INVOKE LISTFILE 'search' .fm (NOH ISODATE~ nw',
   '| stem vml.'                                 /* Hand 2 rexx      */
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
Do m=1 To mdisk.0
  Call SrchDisk
End m
'PIPE (NAME SCANUSR.EXEC:81 END ?)',
    'stem result.',                              /* Take these       */
    '| specs a: 1.45 .',                         /* Add breaks       */
            '46;* 46',                           /* .                */
            'break a',                           /* .                */
            'print a 1',                         /* .                */
    '| > 'myfn id' A'                            /* Write 2 file     */
If result.0<>0 Then Address CMS 'XEDIT 'myfn id' A (WIDTH 500'
 
 
Exit:
/*---------------------------------------------------------------+
| General Exit routine                                           |
+---------------------------------------------------------------*/
Parse Arg exrc errmsg
If Datatype(exrc,'NUM') Then Say errmsg exrc
                        Else exrc=0
Exit exrc
 
 
 
SrchDisk:
/*---------------------------------------------------------------+
| .                                                              |
+---------------------------------------------------------------*/
'PIPE (NAME SCANUSR.EXEC:106 END ?)',
    'cms 'vml.m,                                 /* Run this         */
    '| nlocate anycase ~UNEXPECTED~',            /* Ignore these     */
    '| specs ~'mdisk.m'~ 1.45 1;* nw',           /* Compose output   */
    '| append literal 'Copies('-',45),           /* Add separator    */
    '| stem result. append'                      /* Add to stem      */
Return
